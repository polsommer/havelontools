
/***** INCLUDES ********************************************************/

include library.utils;
include library.static_item;

/***** CONSTANTS *******************************************************/

const string LOVEDAY_CARD_01		= "item_event_loveday_card_01";
const string LOVEDAY_CARD_02		= "item_event_loveday_card_02";
const string LOVEDAY_CARD_03		= "item_event_loveday_card_03";
const string LOVEDAY_CARD_04		= "item_event_loveday_card_04";
const string LOVEDAY_CARD_05		= "item_event_loveday_card_05";
const string LOVEDAY_CARD_06		= "item_event_loveday_card_06";
const string LOVEDAY_CARD_07		= "item_event_loveday_card_07";

const string LOVEDAY_CARDS_STACK		= "item_event_loveday_card_stack";



const string[] LOVEDAY_CARDS_COMPLETE_SET =	{
												LOVEDAY_CARD_01,
												LOVEDAY_CARD_02,
												LOVEDAY_CARD_03,
												LOVEDAY_CARD_04,
												LOVEDAY_CARD_05,
												LOVEDAY_CARD_06,
												LOVEDAY_CARD_07
											};

/***** TRIGGERS *******************************************************/

trigger OnObjectMenuRequest(obj_id player, menu_info mi)
{	
	if (canManipulate(player, self, true, true, 15, true))
	{
		if ( utils.isNestedWithinAPlayer(self) )
		{
			menu_info_data mid = mi.getMenuItemByType(menu_info_types.SERVER_MENU4);
			if ( mid != null )
				mid.setServerNotify(true);
			else
				mi.addRootMenu(menu_info_types.SERVER_MENU4, new string_id("spam", "loveday_cards_bundle"));
		}
	}

	return SCRIPT_CONTINUE;
}

trigger OnObjectMenuSelect(obj_id player, int item)
{
	if( utils.getContainingPlayer(self) != player )
	{
		return SCRIPT_CONTINUE;
	}
	
	if( item == menu_info_types.SERVER_MENU4)
	{
		if( isIncapacitated(player) || isDead(player))
		{
			sendSystemMessage(player, new string_id("player_structure", "while_dead"));
			return SCRIPT_CONTINUE;
		}
		
		resizeable obj_id[] cardsOwned = new obj_id[];
		for ( int i = 0; i < LOVEDAY_CARDS_COMPLETE_SET.length; i++ )
		{
			obj_id lovedaycard = utils.getStaticItemInInventory(player, LOVEDAY_CARDS_COMPLETE_SET[i]);
			if ( isIdValid(lovedaycard) )
			{
				utils.addElement(cardsOwned, lovedaycard);
			}
		}
		
		if ( cardsOwned != null && cardsOwned.length == LOVEDAY_CARDS_COMPLETE_SET.length )
		{
			for ( int j = 0; j < cardsOwned.length; j++ )
			{
				decrementCount(cardsOwned[j]);
			}
			
			obj_id inv = utils.getInventoryContainer(player);
			obj_id cardBundle = static_item.createNewItemFunction(LOVEDAY_CARDS_STACK, inv);
			sendSystemMessage(player, new string_id("spam", "loveday_cards_set_bundled"));
		}
		else
		{
			sendSystemMessage(player, new string_id("spam", "loveday_cards_set_incomplete"));
		}
	}
		
	return SCRIPT_CONTINUE;
}

include library.utils;
include library.sui;

const string SYSTEM_MESSAGES	= "theme_park/warren/warren_system_messages";

const string PASSKEYCODE		= "object/intangible/data_item/warren_evidence_0";

trigger OnObjectMenuRequest(obj_id player, menu_info mi)
{
	if ( hasEncryptionKey( self, player ) )
	{
		sendSystemMessage( player, new string_id(SYSTEM_MESSAGES,"got_evidence") );
		return SCRIPT_CONTINUE;//you've already got one
	}

	mi.addRootMenu (menu_info_types.ITEM_USE, new string_id(SYSTEM_MESSAGES,"get_evidence"));
	return SCRIPT_CONTINUE;
}

trigger OnObjectMenuSelect(obj_id player, int item)
{
	if ( item != menu_info_types.ITEM_USE )
		return SCRIPT_CONTINUE;

	if ( hasEncryptionKey( self, player ) )
		return SCRIPT_CONTINUE;//you've already got one

	sui.msgbox(player, new string_id ( SYSTEM_MESSAGES, "download_complete" ));

	obj_id playerInv = utils.getPlayerDatapad(player);

	int evidencePiece = getIntObjVar( self, "warren.evidence" );
	if (evidencePiece == 0 )
		return SCRIPT_CONTINUE;

	if ( isIdValid( playerInv ) )
	{
		obj_id passKey = createObject( PASSKEYCODE+evidencePiece+".iff", playerInv, "");
		if ( isIdValid( passKey ) )
		{
			//debugSpeakMsg( self, "I made a thing for you" );
			setObjVar( passKey, "warren.evidence", evidencePiece );
		}
	}

	return SCRIPT_CONTINUE;
}

boolean hasEncryptionKey( obj_id terminal, obj_id player )
{
	int evidencePiece = getIntObjVar( terminal, "warren.evidence" );
	if ( evidencePiece == 0 )
	{
		debugSpeakMsg( terminal, "I should have objvar warren.evidence set but I don't" );
		return true;//so we dont try to give the player a bad piece
	}

	obj_id playerInv = utils.getPlayerDatapad(player);

	if ( playerInv == null )
		return false;

	obj_id[] contents = getContents( playerInv );
	if ( contents == null )
		return false;

	for ( int i = 0; i < contents.length; i++ )
	{
		if ( hasObjVar( contents[i], "warren.evidence" ) )
		{
			if ( getIntObjVar( contents[i], "warren.evidence" )==evidencePiece )
			{
				//debugSpeakMsg( terminal, "obj " + contents[i] + " named " + getName( contents[i] ) + " is " + getTemplateName( contents[i] ) );
				//debugSpeakMsg( terminal, "You have evidence" );
				return true;
			}
		}
	}

	return false;
}


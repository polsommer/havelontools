include library.utils;
include library.sui;
include library.colors;

const string SYSTEM_MESSAGES	= "theme_park/warren/warren_system_messages";
const string CONVO_FILE			= "theme_park/warren/warren";

const string ACTIVE = "turret_pass_active";
const string INACTIVE = "turret_pass_inactive";
const string DEACTIVATE = "turret_pass_deactivated";

trigger OnInitialize()
{
	setName( self, "" );
	setName( self, new string_id( SYSTEM_MESSAGES, "turret_term_on" ) );
	return SCRIPT_CONTINUE;
}

trigger OnObjectMenuRequest(obj_id player, menu_info mi)
{
	string stringName = ACTIVE;
	obj_id bldg = getTopMostContainer(self);
	if ( utils.hasScriptVar( bldg, "warren.deactivated") )
	{
		stringName = INACTIVE;
	}

	sui.msgbox(player, new string_id ( SYSTEM_MESSAGES, stringName ));
	return SCRIPT_OVERRIDE;
}

trigger OnHearSpeech(obj_id speaker, string text)
{
	if ( !isPlayer(speaker) )
		return SCRIPT_CONTINUE;

	//already de-activated:
	obj_id bldg = getTopMostContainer(self);
	if ( utils.hasScriptVar( bldg, "warren.deactivated") )
		return SCRIPT_CONTINUE;

	string turretCodeSequence = getStringObjVar( bldg, "warren.turretCodeSequence" );
	if ( turretCodeSequence == null )
		return SCRIPT_CONTINUE;//there is no code

	if ( toUpper(text).equals(turretCodeSequence) )
	{
		setName( self, "" );
		setName( self, new string_id( SYSTEM_MESSAGES, "turret_term_off" ) );

		utils.setScriptVar( bldg, "warren.deactivated", true);
		showFlyText(self, new string_id ( SYSTEM_MESSAGES, DEACTIVATE ), 1.75f, colors.GREENYELLOW);
		messageTo( self, "reactivateTurret", null, 1800, false );
	}

	return SCRIPT_CONTINUE;
}

messageHandler reactivateTurret()
{
	obj_id bldg = getTopMostContainer(self);
	utils.removeScriptVar( bldg, "warren.deactivated");
	setName( self, "" );
	setName( self, new string_id( SYSTEM_MESSAGES, "turret_term_on" ) );
	return SCRIPT_CONTINUE;
}


include library.utils;

const string PASSKEYCODE		= "object/tangible/mission/quest_item/warren_passkey_s02.iff";
const string SYSTEM_MESSAGES	= "theme_park/warren/warren_system_messages";

trigger OnInitialize()
{
	if ( !hasObjVar( self, "warren.passkeyNumber" ))
	{
		debugSpeakMsg( self, "WARNING!  I SHOULD HAVE THE warren.passkeyNumber OBJVAR SET ON ME BUT I DON'T" );
		return SCRIPT_CONTINUE;
	}
	utils.setScriptVar( self, "messageSent", true );
	messageTo( self, "respawnKey", null, 1, false );
	return SCRIPT_CONTINUE;
}

void spawnKey( obj_id container )
{
	obj_id[] contents = getContents( container );

	if ( contents != null )
	{
		for ( int i = 0; i < contents.length; i++)
		{
			if ( hasObjVar( contents[i], "warren.passkeyNumber" ) )
			{
				//remove all the extra keys.
				for(int j = i+1; j < contents.length; ++j)
					destroyObject(contents[j]);
				return; //we have our key
			}
		}
	}
	obj_id passKey = createObject( PASSKEYCODE, container, "" );
	int passKeyNumber = getIntObjVar( container, "warren.passkeyNumber" );
	setObjVar( passKey, "warren.passkeyNumber", passKeyNumber );

	setName( passKey, "" );
	setName( passKey, new string_id( SYSTEM_MESSAGES, "passkey" + passKeyNumber + "_name" ) );

}

messageHandler respawnKey()
{
	utils.removeScriptVar( self, "messageSent" );
	spawnKey( self );
	return SCRIPT_CONTINUE;
}

trigger OnClosedContainer(obj_id player)
{
	if ( utils.hasScriptVar( self, "messageSent" ) )
		return SCRIPT_CONTINUE;//already sent a message to respawn this

	obj_id[] contents = getContents( self );
	if ( contents != null )
	{
		for ( int i = 0; i < contents.length; i++)
		{
			if ( hasObjVar( contents[i], "warren.passkeyNumber" ) )
				return SCRIPT_CONTINUE;//still has a key in it
		}
	}

	messageTo( self, "respawnKey", null, 900, false );//respawns every 15 minutes
	utils.setScriptVar( self, "messageSent", true );
	return SCRIPT_CONTINUE;
}

trigger OnOpenedContainer( obj_id player )
{
	if ( !hasObjVar( self, "warren.passkeyNumber" ))
	{
		debugSpeakMsg( self, "WARNING!  I SHOULD HAVE THE warren.passkeyNumber OBJVAR SET ON ME BUT I DON'T" );
		return SCRIPT_CONTINUE;
	}
	return SCRIPT_CONTINUE;
}

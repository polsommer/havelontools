include library.utils;

const string LETTER_TEMPLATE	= "object/tangible/mission/quest_item/warren_farewell_letter.iff";
const string SYSTEM_MESSAGES	= "theme_park/warren/warren_system_messages";

trigger OnInitialize()
{
	utils.setScriptVar( self, "messageSent", true );
	messageTo( self, "respawnLetter", null, 1, false );
	//spawnLetter( self );
	return SCRIPT_CONTINUE;
}

void spawnLetter( obj_id container )
{
	obj_id[] contents = getContents( container );
	if ( contents != null )
	{
		for ( int i = 0; i < contents.length; i++)
		{
			if ( hasObjVar( contents[i], "warren.letter" ) )
			{
				//remove all the extra letters.
				for(int j = i+1; j < contents.length; ++j)
				{
					destroyObject(contents[j]);
				}
				return; //we have our letter
			}
		}
	}
	obj_id letter = createObject( LETTER_TEMPLATE, container, "" );
	attachScript( letter, "theme_park.warren.goodbye_letter" );
	setObjVar( letter, "warren.letter", true );

	setName( letter, "" );
	setName( letter, new string_id( SYSTEM_MESSAGES, "letter_name" ) );

}

messageHandler respawnLetter()
{
	utils.removeScriptVar( self, "messageSent" );
	spawnLetter( self );
	return SCRIPT_CONTINUE;
}

trigger OnClosedContainer(obj_id player)
{
	if ( utils.hasScriptVar( self, "messageSent" ) )
		return SCRIPT_CONTINUE;//already sent a message to respawn this

	obj_id[] contents = getContents( self );
	if ( contents != null )
	{
		for ( int i = 0; i < contents.length; i++)
		{
			if ( hasObjVar( contents[i], "warren.letter" ) )
				return SCRIPT_CONTINUE;//still has a letter in it
		}
	}

	messageTo( self, "respawnLetter", null, 3600, false );//respawns every hour
	utils.setScriptVar( self, "messageSent", true );
	return SCRIPT_CONTINUE;
}

trigger OnOpenedContainer( obj_id player )
{

	obj_id[] contents = getContents( self );
	if ( contents != null )
	{
		for ( int i = 0; i < contents.length; i++)
		{
			if ( hasObjVar( contents[i], "warren.letter" ) )
				return SCRIPT_CONTINUE;//still has a letter in it
		}
	}

	contents = getContents( utils.getInventoryContainer( player ) );
	if ( contents != null )
	{
		for ( int i = 0; i < contents.length; i++)
		{
			if ( hasObjVar( contents[i], "warren.letter" ) )
				return SCRIPT_CONTINUE;//this player already has a letter
		}
	}

	spawnLetter( self );

	return SCRIPT_CONTINUE;
}


include library.utils;
include library.sui;
include library.colors;

const string SYSTEM_MESSAGES	= "theme_park/warren/warren_system_messages";

const string ACTIVE = "cell_locked";
const string INACTIVE = "cell_open";
const string DEACTIVATE = "cell_unlocked";

trigger OnObjectMenuRequest(obj_id player, menu_info mi)
{
	string stringName = ACTIVE;
	obj_id bldg = getTopMostContainer(self);
	if ( utils.hasScriptVar( bldg, "warren.cellOpened") )
	{
		stringName = INACTIVE;
	}

	sui.msgbox(player, new string_id ( SYSTEM_MESSAGES, stringName ));

	//this is just testing stuff, do not uncomment or I'll hit you
	//messageTo( self, "lockDoor", null, 10, false );
	//showFlyText(self, new string_id ( SYSTEM_MESSAGES, INACTIVE ), 1.75f, colors.GREENYELLOW);
	//debugSpeakMsg( player, getStringObjVar( bldg, "warren.cellPassword" ) );
	return SCRIPT_OVERRIDE;
}

trigger OnInitialize()
{
	messageTo( self, "lockDoor", null, 1800, false );
	return SCRIPT_CONTINUE;
}

trigger OnHearSpeech(obj_id speaker, string text)
{
	if ( !isPlayer(speaker) )
		return SCRIPT_CONTINUE;

	//already de-activated:
	obj_id bldg = getTopMostContainer(self);
	if ( utils.hasScriptVar( bldg, "warren.cellOpened") )
		return SCRIPT_CONTINUE;

	string turretCodeSequence = getStringObjVar( bldg, "warren.cellPassword" );
	if ( turretCodeSequence == null )
		return SCRIPT_CONTINUE;//there is no code

	if ( toUpper(text).equals(turretCodeSequence) )
	{
		utils.setScriptVar( bldg, "warren.cellOpened", true);
		showFlyText(self, new string_id ( SYSTEM_MESSAGES, INACTIVE ), 1.75f, colors.GREENYELLOW);
		permissionsMakePublic( getCellId( getTopMostContainer(self), "smallroom47") );
		messageTo( self, "lockDoor", null, 1800, false );
	}

	return SCRIPT_CONTINUE;
}

messageHandler lockDoor()
{
	obj_id bldg = getTopMostContainer(self);
	utils.removeScriptVar( bldg, "warren.cellOpened");
	permissionsMakePrivate( getCellId( getTopMostContainer(self), "smallroom47") );
	return SCRIPT_CONTINUE;
}


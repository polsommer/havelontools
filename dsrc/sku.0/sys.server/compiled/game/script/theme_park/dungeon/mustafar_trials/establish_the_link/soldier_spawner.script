include library.ai_lib;
include library.utils;
include library.create;
include library.trial;

const string BUG_COUNT					= "spawning.bugCount";
const int BUG_MAX					= 3;

const string SOLDIER					= "som_link_lava_beetle_soldier";

const boolean LOGGING					= false;

trigger OnAttach()
{
	messageTo (self, "spawnNewBug", null, 5, false);
	locatePatrolPoints(self);
	return SCRIPT_CONTINUE;
}

void locatePatrolPoints(obj_id self)
{
	obj_id contents[] = utils.getSharedContainerObjects(self);
	if (contents == null || contents.length == 0)
	{
		doLogging("findWayPoints", "Contents list was empty, exiting");
		return;
	}
	
	resizeable location[] waypoints = new location[0];
	
	for (int i = 0; i < contents.length; i++)
	{
		if (utils.hasScriptVar(contents[i], trial.WP_NAME))
		{
			utils.addElement(waypoints, getLocation(contents[i]));
		}
	}
	
	if (waypoints == null)
	{
		doLogging("findWayPoints", "No waypoints were found, exiting");
		return;
	}
	
	location[] patrolPoints = waypoints;
	if (patrolPoints.length == 0)
	{
		doLogging("findWayPoints", "Patrol Point list was empty, exiting");
		return;
	}
	
	utils.setScriptVar(self, "patrolPoints", patrolPoints);

}

messageHandler spawnNewBug()
{
	int randX = rand(-6, 6);
	int randZ = rand(-6, 6);
	location[] patrolPoints = utils.getLocationArrayScriptVar(self, "patrolPoints");
	location spawnLoc = patrolPoints[rand(0, patrolPoints.length -1)];
	spawnLoc.x += randX;
	spawnLoc.z += randZ;
	
	obj_id creature = create.object (SOLDIER, spawnLoc);
	attachScript(creature, "theme_park.dungeon.mustafar_trials.establish_the_link.soldier_spawner_tracker");
	utils.setScriptVar(creature, trial.PARENT, self);
	incrementBugCount(self);
	if (canSpawnMoreBugs(self))
	{
		messageTo (self, "spawnNewBug", null, 20, false);
	}
	return SCRIPT_CONTINUE;
}

int incrementBugCount(obj_id self)
{
	int count = 0;
	if (utils.hasScriptVar(self, BUG_COUNT))
		count = utils.getIntScriptVar(self, BUG_COUNT);
	
	count += 1;
	utils.setScriptVar(self, BUG_COUNT, count);
	return count;
}

int decrementBugCount(obj_id self)
{
	int count = 0;
	if (utils.hasScriptVar(self, BUG_COUNT))
		count = utils.getIntScriptVar(self, BUG_COUNT);
	
	count = count - 1;
	if (count < 0)
		count = 0;
		
	utils.setScriptVar(self, BUG_COUNT, count);
	
	return count;
}

boolean canSpawnMoreBugs(obj_id self)
{
	if (!utils.hasScriptVar(self, BUG_COUNT))
	{
		return true;
	}
	
	int currentCount = utils.getIntScriptVar(self, BUG_COUNT);
	
	if (currentCount < BUG_MAX)
	{
		return true;
	}
	
	doLogging("canSpawnMoreBugs", "Bug max exceeded, stop spawning bugs");
	return false;
}

messageHandler soldierDied()
{
	decrementBugCount(self);
	
	if (canSpawnMoreBugs(self))
	{
		messageTo(self, "spawnNewBug", null, 0, false);
		return SCRIPT_CONTINUE;
	}
	else
	{
		doLogging("soldierDied", "Cannot spawn more bugs because limit has been reached");
		return SCRIPT_CONTINUE;
	}
}

void doLogging(string section, string message)
{
	if (LOGGING || trial.UPLINK_LOGGING)
		LOG("logging/soldier_spawner/"+section, message);	
}
include library.ai_lib;
include library.utils;
include library.trial;
include library.create;


const string SQUAD_MEMBER				= "som_battlefield_droid_soldier";
const boolean LOGGING					= false;

trigger OnAttach()
{
	findWayPoints(self);
	messageTo (self, "pathToNextPoint", null, 2, false);
	trial.setInterest(self);
	messageTo(self, "spawnSquad", null, 2, false);
	trial.markAsDroidArmy(self);	
	setHibernationDelay(self, 7200);
	return SCRIPT_CONTINUE;
}

trigger OnIncapacitated(obj_id killer)
{
	trial.prepareCorpse(self);
	utils.setScriptVar(self, trial.BATTLEFIELD_DROID_CORPSE, true);	
	return SCRIPT_CONTINUE;
}

messageHandler spawnSquad()
{
	obj_id[] squad = new obj_id[4];
	location baseLoc = getLocation(self);
	setYaw(self, -250);
	
	for (int i=0;i<squad.length;i++)
	{
		location spawnLoc = new location(baseLoc.x, baseLoc.y, baseLoc.z, baseLoc.area, baseLoc.cell);
		squad[i] = create.object(SQUAD_MEMBER, spawnLoc);
		
		if (isIdValid(squad[i]))
		{
			attachScript(squad[i], "theme_park.dungeon.mustafar_trials.valley_battleground.droid_squad_member");
			utils.setScriptVar(squad[i], trial.PARENT, self);
			//ai_lib.followInFormation(squad[i], self, ai_lib.FORMATION_WEDGE, i+1);
			utils.setScriptVar(squad[i], "patrolPoints", utils.getLocationArrayScriptVar(self, "patrolPoints"));
			trial.markAsDroidArmy(squad[i]);
		}
	}
	
	//ai_lib.establishAgroLink(self, squad);
	for (int j=0;j<squad.length;j++)
	{
		messageTo(squad[j], "pathToNextPoint", null, j+2, false);
	}
	return SCRIPT_CONTINUE;

}

void findWayPoints(obj_id self)
{
	obj_id objects[] = getObjectsInRange(self, 400);
	if (objects == null || objects.length == 0)
	{
		doLogging("findWayPoints", "Contents list was empty, exiting");
		return;
	}
	
	int pathNum = 0;
	
	if (utils.hasScriptVar(self, "path"))
	{		
		pathNum = utils.getIntScriptVar(self, "path");
	}
	else
	{
		string[] paths = dataTableGetStringColumn(trial.VALLEY_DATA, "path");
		pathNum = rand(0, paths.length -1);
	}
	
	string path = dataTableGetString(trial.VALLEY_DATA, pathNum, "path");
	string[] pathList = split(path, ';');
	
	if (pathList == null || pathList.length == 0)
	{
		doLogging("findWayPoints", "Path list was empty, exiting");
		return;
	}
	resizeable location[] waypoints = new location[0];

	for (int i=0;i<pathList.length;i++)
	{
		for (int k=0;k<objects.length;k++)
		{
			if (hasObjVar(objects[k], "wp_name"))
			{
				if (pathList[i].equals(getStringObjVar(objects[k], "wp_name")))
				{
					utils.addElement(waypoints, getLocation(objects[k]));
				}
			}
		}
	}
	
	
	if (waypoints == null)
	{
		doLogging("findWayPoints", "No waypoints were found, exiting");
		return;
	}
	
	location[] patrolPoints = waypoints;
	if (patrolPoints.length == 0)
	{
		doLogging("findWayPoints", "Patrol Point list was empty, exiting");
		return;
	}
	
	utils.setScriptVar(self, "patrolPoints", patrolPoints);
	
}

messageHandler pathToNextPoint()
{
	location[] patrolPoints = utils.getLocationArrayScriptVar(self, "patrolPoints");
	ai_lib.setPatrolOncePath(self, patrolPoints);

	return SCRIPT_CONTINUE;
}

void doLogging(string section, string message)
{
	if (LOGGING || trial.VALLEY_LOGGING)
		LOG("logging/droid_squad_leader/"+section, message);	
}
// Obiwan event re-launch delay script - Keeps the player from re-attempting the mustafar obiwan finale quest if the delay time period hasn't elapsed yet.

// ********************************************************************
//					INCLUDES
// ********************************************************************

// ********************************************************************
//					CONSTS
// ********************************************************************

const int REATTEMPT_DELAY_MINUTES		= 120;	//length of delay in minutes before player can reattempt obiwan quest, after failing/quitting.

// ********************************************************************
//					TRIGGERS
// ********************************************************************

trigger OnAttach ()
{
	initializeTimeDelay(self);
	return SCRIPT_CONTINUE;
}

//----------------------------------------------------------------------

trigger OnInitialize ()
{
	mustafarReattemptDelayCheck(self);
	return SCRIPT_CONTINUE;
}

//----------------------------------------------------------------------

trigger OnLogin()
{
	mustafarReattemptDelayCheck(self);
	return SCRIPT_CONTINUE;
}

// ********************************************************************
//					FUNCTIONS
// ********************************************************************

void initializeTimeDelay(obj_id player)
{

	int currentTime = getGameTime();
	int delayTime = currentTime + (REATTEMPT_DELAY_MINUTES*60);
	setObjVar(player, "mustafar.reattemptDelay", delayTime);

	return;
}

//----------------------------------------------------------------------

void mustafarReattemptDelayCheck(obj_id player)
{
	if (!hasObjVar(player,"mustafar.reattemptDelay"))
	{
		detachScript(player, "player_reattempt_delay");
		return;
	}

	int timeOutEnd = getIntObjVar(player, "mustafar.reattemptDelay");
	int currentTime = getGameTime();
	if (currentTime < timeOutEnd)
	{
		int delayTimeRemaining = timeOutEnd - currentTime;
		if (delayTimeRemaining < 60)	// don't bother with a delay message if the delay times out in only a minute.
			return;
		
		messageTo( player, "mustafarReattemptTimeoutExpire", null, delayTimeRemaining, false );
		return;
	}

	removeObjVar(player, "mustafar.reattemptDelay");
	detachScript(player,"them_park.dungeon.mustafar_trials.obiwan_finale.player_reattempt_delay");
	return;
}

// ********************************************************************
//					MESSAGE HANDLERS
// ********************************************************************

messageHandler mustafarReattemptTimeoutExpire()
{
	mustafarReattemptDelayCheck(self);
	return SCRIPT_CONTINUE;
}


include library.utils;
include library.create;
include library.groundquests;
include library.chat;
include library.space_utils;
include library.prose;
include library.player_structure;

const string STF									= "dungeon/avatar_platform";
const string TBL_BOSS_FIGHT							= "datatables/dungeon/avatar_platform/ep3_avatar_platform_boss_trando.iff";
const string_id TAUNT								= new string_id(STF, "harwakokok_taunt");

trigger OnReceivedItem (obj_id destinationCell, obj_id transferrer, obj_id item)
{
	if (!isPlayer (item))
	{
		return SCRIPT_CONTINUE;
	}

	if(!groundquests.isTaskActive(item, "ep3_trando_hssissk_zssik_10", "killHarwakokok"))
	{
		//debugSpeakMsg(item, "I don't have correct quest");
		return SCRIPT_CONTINUE;
	}

	obj_id structure = getTopMostContainer (self);
	obj_id commanddeck = getCellId(structure, "commanddeck");
	//debugSpeakMsg(item, "This structure is " + structure);
	//obj_id speaker = getObjId(structure, "boss_fight_speaker");

	if(hasObjVar(structure, "avatar_platform.boss_fight"))
	{
		return SCRIPT_CONTINUE;
	}

	spawnBossFight(commanddeck, item);
	beginTaunt(self, item);
	setObjVar(structure, "avatar_platform.boss_fight", 1);

	return SCRIPT_CONTINUE;
}

void spawnBossFight (obj_id commanddeck, obj_id player)
{
	obj_id self = getSelf();
	obj_id structure = getTopMostContainer (self);

	int defenderCreatures = dataTableGetNumRows (TBL_BOSS_FIGHT);
	int x = 0;

	while (x < defenderCreatures)
	{
		string spawn = dataTableGetString (TBL_BOSS_FIGHT, x, "spawns");

		float xCoord = dataTableGetFloat (TBL_BOSS_FIGHT, x, "loc_x");
		float yCoord = dataTableGetFloat (TBL_BOSS_FIGHT, x, "loc_y");
		float zCoord = dataTableGetFloat (TBL_BOSS_FIGHT, x, "loc_z");

		location myself = getLocation (player);

		string planet = myself.area;
		obj_id top = getTopMostContainer (player);
		string spawnRoom = dataTableGetString (TBL_BOSS_FIGHT, x, "room");
		obj_id room = getCellId (structure, spawnRoom);

		location spawnPoint = new location (xCoord, yCoord, zCoord, planet, room);

		obj_id spawnedCreature = create.object (spawn, spawnPoint);
		if(spawn == "ep3_avatar_harwakokok_mighty")
		{
			attachScript(spawnedCreature, "theme_park.dungeon.avatar_platform.avatar_boss_clean_up");
		}
		else
		{
			attachScript(spawnedCreature, "theme_park.dungeon.avatar_platform.avatar_minon_clean_up");
		}

		x = x + 1;
	}
	return;
}

void beginTaunt(obj_id self, obj_id player)
{
	obj_id structure = getTopMostContainer(self);
	obj_id[] players = player_structure.getPlayersInBuilding(structure);

	if (players == null )
	{
		//debugSpeakMsg(player, "Players are null");
		return;
	}

	int numPlayers = players.length;
	//debugSpeakMsg(player, "numPlayers is " + numPlayers);

	if (numPlayers > 0)
	{
		for (int i = 0; i < numPlayers; i++)
		{
			groundquests.grantQuest(players[i], "ep3_avatar_boss_taunt");
		}
	}

	messageTo(self, "handleOpenDoor", null, 10f, false);
	return;
}

messageHandler handleOpenDoor ()
{
	obj_id structure = getTopMostContainer (self);
	obj_id commanddeck = getCellId(structure, "commanddeck");

	permissionsMakePublic(commanddeck);

	return SCRIPT_CONTINUE;
}
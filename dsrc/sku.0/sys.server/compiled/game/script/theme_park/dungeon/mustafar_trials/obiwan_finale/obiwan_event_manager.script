// Obiwan event manager - overall controller/initializer for obiwan event.
//This script borrows heavily from THicks link event manager script


// ********************************************************************
//					INCLUDES
// ********************************************************************

include library.ai_lib;
include library.buff;
include library.chat;
include library.mustafar;
include library.prose;
include library.static_item;
include library.trial;
include library.utils;
include library.badge;
include library.space_dungeon;


// ********************************************************************
//					CONSTS
// ********************************************************************

const string STF_OBI_CONVO						= "conversation/som_kenobi_obi_wan";

const string_id SID_LAUNCH 						= new string_id(mustafar.STF_OBI_MSGS, "obiwan_finale_launch");
const string_id SID_EJECT 						= new string_id(mustafar.STF_OBI_MSGS, "obiwan_finale_eject");
const string_id SID_LEAVE_YOUR_GROUP		= new string_id(mustafar.STF_OBI_MSGS, "obi_leave_your_group");

// debug tools

const boolean CONST_FLAG_DO_LOGGING		= false;

// ********************************************************************
//					TRIGGERS
// ********************************************************************

trigger OnHearSpeech(obj_id speaker, string text)
{
	if(isGod(speaker))
	{
		if (text.equals("reset"))
		{
			clearEventArea(self);
		}
	}
	return SCRIPT_CONTINUE;
}

// ********************************************************************
//					FUNCTIONS
// ********************************************************************



void debugLogging(string section, string message)
{
	if (CONST_FLAG_DO_LOGGING)
		LOG("debug/obiwan_event_manager/"+section, message);
}


//----------------------------------------------------------------------

void clearEventArea(obj_id dungeon)
{
	obj_id cell = getCellId(dungeon, "mainroom");			// <- name of instanced dungeon's cell

	obj_id[] contents = getContents(cell);
	if (contents == null || contents.length == 0)
	{
		debugLogging("clearEventArea", "Dungeon was empty, return");
		return;
	}
	for (int i = 0; i<contents.length; i++)
	{
		if (isPlayer(contents[i]))
		{
			debugLogging("clearEventArea", "Validating player("+getName(contents[i])+"/"+contents[i]+") from event area");
		}
		else
		{
			debugLogging("clearEventArea", "Destroying object("+getName(contents[i])+"/"+contents[i]+")");
			trial.cleanupNpc(contents[i]);
		}
	}

	cleanupDungeonScriptvars();

}

//----------------------------------------------------------------------

void eventWon()
{
	obj_id self = getSelf();
	int newEndTime = getGameTime() + 120;
	setObjVar(self, space_dungeon.VAR_DUNGEON_END_TIME, newEndTime);
	debugLogging("darkJediEventWon", "Event won triggered");

	obj_id player = utils.getObjIdScriptVar(self, "player");
	setObjVar(player, "didMustafarCrystalLair", 1);					// You can NEVER do this quest again.

	if(hasScript(player,"theme_park.dungeon.mustafar_trials.obiwan_finale.obiwan_quest_monitor"))			// this is a slow script that's no longer needed, so dump it.
		detachScript(player,"theme_park.dungeon.mustafar_trials.obiwan_finale.obiwan_quest_monitor");

	if (hasObjVar(player,"sawObiwanAtLauncher"))
		removeObjVar(player,"sawObiwanAtLauncher");

	return;
}

//----------------------------------------------------------------------

void cleanupDungeonScriptvars()
{
	obj_id self = getSelf();

	if (utils.hasScriptVar(self,"minions"))		// clear out the previous minion count.
		utils.removeScriptVar(self,"minions");

	if (utils.hasScriptVar(self,"obiLocation"))		// clear out the previous minion count.
		utils.removeScriptVar(self,"obiLocation");

	if (utils.hasScriptVar(self,"crystal"))
		utils.removeScriptVar(self,"crystal");

	if (utils.hasScriptVar(self,"obiwan"))
		utils.removeScriptVar(self,"obiwan");

	if (utils.hasScriptVar(self,"player"))
		utils.removeScriptVar(self,"player");

	if (utils.hasScriptVar(self,"darkJedi"))
		utils.removeScriptVar(self,"darkJedi");


}

// ********************************************************************
//					MESSAGE HANDLERS
// ********************************************************************

// entry from this script

messageHandler dungeonCleanup()
{
	clearEventArea(self);
	return SCRIPT_CONTINUE;
}

messageHandler cleanupSpawn()
{
	clearEventArea(self);
	return SCRIPT_CONTINUE;
}
//----------------------------------------------------------------------
// entry from this script

messageHandler beginSpawn()
{
	clearEventArea(self);
	mustafar.spawnContents(self, "setpiece", 1);
	obj_id player = utils.getObjIdScriptVar(self, "player");

	return SCRIPT_CONTINUE;
}

//----------------------------------------------------------------------
// entry from obiwan_jedi_crystal_stand

messageHandler obiwanSpeechDelay()
// Obiwan, standing near the crystal the player has approached, gives the player the rundown on it.
// When done, there's a pause, and the Dark Jedi spawns for the first time, to confront the player, and ultimately fight.
{
	obj_id player = utils.getObjIdScriptVar(self, "player");
	obj_id obiwan = utils.getObjIdScriptVar(self, "obiwan");

	faceTo(obiwan, player);
	string_id strSpam = new string_id(mustafar.STF_OBI_MSGS, "som_obi_crystal_speech1");		//obi gives player speech about crystal here.
	prose_package pp = prose.getPackage(strSpam);
	prose.setTT( pp, player );
	chat.chat (obiwan, player, pp);

	messageTo(self, "obiwanSpeechContinued", params, 5, false);	//This messagehandler sets up the delay between the first part of Obi's speech at the stand, and the second
	return SCRIPT_CONTINUE;
}

//----------------------------------------------------------------------
// entry from this script

messageHandler obiwanSpeechContinued()
// Obiwan, standing near the crystal the player has approached, gives the player the rundown on it.
// When done, there's a pause, and the Dark Jedi spawns for the first time, to confront the player, and ultimately fight.
{
	obj_id player = utils.getObjIdScriptVar(self, "player");
	obj_id obiwan = utils.getObjIdScriptVar(self, "obiwan");

	faceTo(obiwan, player);
	string_id strSpam = new string_id(mustafar.STF_OBI_MSGS, "som_obi_crystal_speech2");		//obi gives player speech about crystal here.
	prose_package pp = prose.getPackage(strSpam);
	prose.setTT( pp, player );
	chat.chat (obiwan, player, pp);

	debugLogging("//***// obiwanSpeechContinued: ", "////>>>> calling mustafar.spawnContents for 'boss'.");
	messageTo(self, "spawnBossDelay", params, 1, false);		//The dark jedi spawns just as Obi finishes up talking about the crystal!
	return SCRIPT_CONTINUE;
}

//----------------------------------------------------------------------
// entry from this script

messageHandler lightsCameraAction()
// The dark jedi spawns and confronts the player about wanting his lucky charms... er... the jedi crystal.
{
	obj_id player = utils.getObjIdScriptVar(self, "player");
	obj_id obiwan = utils.getObjIdScriptVar(self, "obiwan");
	obj_id darkJedi = utils.getObjIdScriptVar(self, "darkJedi");
	int intermissionNum = utils.getIntScriptVar(self, "intermission");

	if(utils.hasScriptVar(player,"readyToUseCrystal"))
		utils.removeScriptVar(player,"readyToUseCrystal");
	if(utils.hasScriptVar(player,"dealWithCrystal"))
		utils.removeScriptVar(player,"dealWithCrystal");

	debugLogging(" lightsCameraAction: ", "////>>>> entered 'intermission' lightsCameraAction with intermissionNum of: "+intermissionNum);
	debugLogging(" lightsCameraAction: ", "////>>>> player objId is: "+player+" obiwan objId is: "+obiwan+" and darkjedi objid is: "+darkJedi);

	chat.setChatMood( darkJedi, chat.MOOD_MALEVOLENT );
	chat.setChatType( darkJedi, chat.CHAT_HOWL );

	if (intermissionNum == 0)
	// First intermission. The dark jedi's first taunts, get away from my crystal, etc
	{
		debugLogging(" lightsCameraAction: ", "////>>>> Doing IntermissionNumber -0-");

		playClientEffectObj(player, "clienteffect/mustafar/som_dark_jedi_laugh.cef", player, "");

		doAnimationAction(darkJedi, "threaten");
		string_id strSpam = new string_id(mustafar.STF_OBI_MSGS, "som_dark_jedi_crystal_speech1");
		prose_package pp = prose.getPackage(strSpam);
		pp.target.set (player);
		chat.chat (darkJedi, player, pp);	// hurl insults. It's mine! The crystal is mine! One ring to rule them all!

		messageTo(self, "darkJediThrowsDownPartOne", params, 10, false);
	}
	else if (intermissionNum == 1)
	// Second intermission. The dark jedi's second taunts, Obiwan warns the jedi, etc
	{
		debugLogging(" lightsCameraAction: ", "////>>>> Doing IntermissionNumber -1-");

		//chat.setAngryMood( darkJedi );

		// darkJedi gets angrier and hurls more insults.
		doAnimationAction(darkJedi, "point_forward");
		string_id strSpam = new string_id(mustafar.STF_OBI_MSGS, "som_dark_jedi_cannot_defeat_me");
		prose_package pp = prose.getPackage(strSpam);
		prose.setTT( pp, player );
		chat.chat (darkJedi, player, pp);

		messageTo(self, "obiSaysBeCareful", null, 3, false);	//obi says be careful. In a wise, wise way.

		messageTo(self, "moveBossToHomeLoc", null, 10, false);

		dictionary waveParams = new dictionary();
		waveParams.put("waveNum", 2);
		messageTo(self, "minionWaveLaunch", waveParams, 6, false);
			}
	else if (intermissionNum == 2)
	// Second intermission. The dark jedi's second taunts, Obiwan warns the jedi, etc
	{
		debugLogging(" lightsCameraAction: ", "////>>>> Doing IntermissionNumber -2-");

		//playMusic (player, "sound/mus_duel_of_the_fates_lcv.snd");

		// darkJedi shreaks that he will destroy the player himself!.
		string_id strSpam = new string_id(mustafar.STF_OBI_MSGS, "som_dark_jedi_destroy_you_myself");
		prose_package pp = prose.getPackage(strSpam);
		pp.target.set (player);
		chat.chat (darkJedi, player, pp);

		utils.setScriptVar(player, "readyToUseCrystal", 1);
		messageTo(self, "moveObiwanForCrystalComment", null, 1, false);	//obi moves to say 'gee, he's strong. Use the crystal'
		//messageTo(self, "obiSaysBeCareful2", null, 2, false);	//obi says 'gee, he's strong. Use the crystal'

		messageTo(self, "darkJediThrowsDownPartTwo", null, 17, false);		//Dark Jedi says, 'I'll snap you in two!'
		messageTo(darkJedi, "startFighting", null, 23, false);
	}
	else if (intermissionNum == 3)
	// Third intermission. Dark Jedi survived because we cheated. More talking, and more minions.
	{
		debugLogging(" lightsCameraAction: ", "////>>>> Doing IntermissionNumber -3-");


		messageTo(self, "moveBossToHomeLoc", null, 1, false);

		dictionary waveParams = new dictionary();
		waveParams.put("waveNum", 3);
		messageTo(self, "minionWaveLaunch", waveParams, 10, false);
	}
	else if (intermissionNum == 4)
	// Fourth intermission. Dark Jedi is coming one last time, and it's on like Donkey Kong!.
	{
		debugLogging(" lightsCameraAction: ", "////>>>> Doing IntermissionNumber -4-");

		chat.setChatMood( darkJedi, chat.MOOD_EXASPERATED );

		// darkJedi shreaks that he will destroy the player himself!.
		string_id strSpam = new string_id(mustafar.STF_OBI_MSGS, "som_dark_jedi_nooo");
		prose_package pp = prose.getPackage(strSpam);
		pp.target.set (player);
		chat.chat (darkJedi, player, pp);

		// final fight begins. Probably ought to buff the dark jedi some here.								<-Re-examine this.
		messageTo(darkJedi, "startFighting", null, 10, false);
	}
	else if (intermissionNum == 5)
	// Last intermission. You've defeated the dark jedi. Now it's time to decide what to do about the crystal.
	{
		debugLogging(" lightsCameraAction: ", "////>>>> Doing IntermissionNumber -5-");

		if(utils.hasScriptVar(player,"readyToUseCrystal"))
			utils.removeScriptVar(player,"readyToUseCrystal");
		utils.setScriptVar(player,"dealWithCrystal", 1);
		buff.removeAllBuffs(player);		//done with the fight. Yoink the mega-buffs.

		faceTo(obiwan, player);
		string_id strSpam = new string_id(mustafar.STF_OBI_MSGS, "som_obi_won_congrats");	//obi gives player another speech about crystal here.
		prose_package pp = prose.getPackage(strSpam);
		prose.setTT( pp, player );
		chat.chat (obiwan, player, pp);

		messageTo(self, "obiSaysDestroyCrystal", null, 3, false);

	}
	else if (intermissionNum > 5)
	{
		debugLogging(" lightsCameraAction: ", "////>>>> ERROR. RECEIVED AN INTERMISSION VALUE GREATER THAN  -5-");
		return SCRIPT_CONTINUE;
	}
	intermissionNum++;
	utils.setScriptVar(self, "intermission", intermissionNum); // increment intermission counter

	return SCRIPT_CONTINUE;
}

//----------------------------------------------------------------------
// entry from this script

messageHandler spawnBossDelay()
// The Dark Jedi, having told player to get away from his crystal, runs back to his corner and summons minions
{
	obj_id player = utils.getObjIdScriptVar(self, "player");
	obj_id darkJedi = utils.getObjIdScriptVar(self, "darkJedi");
	debugLogging("//***// spawnBossDelay: ", "////>>>> player objid: "+player+" and darkJedi objid: "+darkJedi);

	mustafar.spawnContents(self, "boss", 1);

	return SCRIPT_CONTINUE;
}
//----------------------------------------------------------------------
// entry from this script

messageHandler darkJediThrowsDownPartOne()			// The Dark Jedi, having told player to get away from his crystal, runs back to his corner and summons minions
{
	obj_id player = utils.getObjIdScriptVar(self, "player");
	obj_id darkJedi = utils.getObjIdScriptVar(self, "darkJedi");
	debugLogging("//***// darkJediThrowsDownPartOne: ", "////>>>> player objid: "+player+" and darkJedi objid: "+darkJedi);

	dictionary waveParams = new dictionary();
	waveParams.put("waveNum", 1);
	messageTo(self, "moveBossToHomeLoc", null, 14, false);
	messageTo(self, "minionWaveLaunch", waveParams, 10, false);

	return SCRIPT_CONTINUE;
}

//----------------------------------------------------------------------
// entry from this script

messageHandler obiSaysBeCareful()			// The Dark Jedi, having told player to get away from his crystal, runs back to his corner and summons minions
{
	obj_id player = utils.getObjIdScriptVar(self, "player");
	obj_id obiwan = utils.getObjIdScriptVar(self, "obiwan");

	faceTo(obiwan, player);
	string_id strSpam = new string_id(mustafar.STF_OBI_MSGS, "som_obi_be_careful");	//Obiwan is very, very wise.
	prose_package pp = prose.getPackage(strSpam);
	pp.target.set (player);
	chat.chat (obiwan, player, pp);

	return SCRIPT_CONTINUE;
}

//----------------------------------------------------------------------
// entry from this script

messageHandler obiSaysBeCareful2()
{
	obj_id player = utils.getObjIdScriptVar(self, "player");
	obj_id obiwan = utils.getObjIdScriptVar(self, "obiwan");

	faceTo(obiwan, player);
	string_id strSpam = new string_id(mustafar.STF_OBI_MSGS, "som_obi_be_careful2");	//Obiwan is very, very wise.
	prose_package pp = prose.getPackage(strSpam);
	pp.target.set (player);
	chat.chat (obiwan, player, pp);

	messageTo(self, "moveObiwanHomeAfterCommenting", null, 5, false);

	return SCRIPT_CONTINUE;
}

//----------------------------------------------------------------------
// entry obiwan_lair_boss

messageHandler obiwanWarnsOfSpecialAttack()
{
	debugLogging("//***// obiwanWarnsOfSpecialAttack: ", "////>>>> -------------------------------	ENTERED");
	if (!utils.hasScriptVar(self,"darkJedi"))
		return SCRIPT_CONTINUE;

	obj_id player = utils.getObjIdScriptVar(self, "player");
	obj_id obiwan = utils.getObjIdScriptVar(self, "obiwan");

	if (!utils.hasScriptVar(self,"darkJedi"))
		return SCRIPT_CONTINUE;

	faceTo(obiwan, player);
	string_id strSpam = new string_id(mustafar.STF_OBI_MSGS, "som_obi_lookout_special");	//Obiwan is very, very wise.
	prose_package pp = prose.getPackage(strSpam);
	pp.target.set (player);
	chat.chat (obiwan, player, pp);

	return SCRIPT_CONTINUE;
}


//----------------------------------------------------------------------
// entry obiwan_lair_boss

messageHandler obiwanCongratsSpecialAttackBlock()
{
	debugLogging("//***// obiwanCongratsSpecialAttackBlock: ", "////>>>> -------------------------------	ENTERED");
	if (!utils.hasScriptVar(self,"darkJedi"))
		return SCRIPT_CONTINUE;

	obj_id player = utils.getObjIdScriptVar(self, "player");
	obj_id obiwan = utils.getObjIdScriptVar(self, "obiwan");

	faceTo(obiwan, player);
	string_id strSpam = new string_id(mustafar.STF_OBI_MSGS, "som_obi_block_special");	//Obiwan is very, very wise.
	prose_package pp = prose.getPackage(strSpam);
	pp.target.set (player);
	chat.chat (obiwan, player, pp);

	return SCRIPT_CONTINUE;
}

//----------------------------------------------------------------------
// entry from this script

messageHandler darkJediThrowsDownPartTwo()			// wrings his knuckles and tells the young jedi that he's in for it.
{
	obj_id player = utils.getObjIdScriptVar(self, "player");
	obj_id darkJedi = utils.getObjIdScriptVar(self, "darkJedi");

	setObjVar(darkJedi, "randomTaunt", 1);					// enable the random taunter

	faceTo(darkJedi, player);
	string_id strSpam = new string_id(mustafar.STF_OBI_MSGS, "som_dark_jedi_snap_you_half");		//dark jedi meanaces, menacingly
	prose_package pp = prose.getPackage(strSpam);
	prose.setTT( pp, player );
	chat.chat (darkJedi, player, pp);

	return SCRIPT_CONTINUE;
}

//----------------------------------------------------------------------
// entry from this script

messageHandler minionWaveLaunch()
{
	debugLogging("minionWaveLaunch", "entered");
	int waveNumber = params.getInt("waveNum");
	if (waveNumber < 0)
	{
		debugLogging("minionWaveLaunch", "waveNumber was less than zero");
		debugLogging("//***// minionWaveLaunch: ", "////>>>> messaging ------------------------------- 'lightsCameraAction.'");
		messageTo (self, "lightsCameraAction", null, 1, false);	//bad data. Cue the next intermission.
		return SCRIPT_CONTINUE;
	}
	debugLogging("minionWaveLaunch", "waveNumber is: "+waveNumber);

	obj_id player = utils.getObjIdScriptVar(self, "player");
	obj_id darkJedi = utils.getObjIdScriptVar(self, "darkJedi");

	// darkJedi beckons forth his minions.
	faceTo(darkJedi, player);
	string_id strSpam = new string_id(mustafar.STF_OBI_MSGS, "som_dark_jedi_attack_minions_"+waveNumber);		//Dark Jedi screams "attack, my minions!" or something equally dramatical.
	prose_package pp = prose.getPackage(strSpam);
	pp.target.set (player);
	chat.chat (darkJedi, player, pp);

	if (utils.hasScriptVar(self,"minions"))		//since we're launching a fresh wave of badguys, clear out the previous count.
		utils.removeScriptVar(self,"minions");


	switch(waveNumber)
	{
		case 1:
			{
			mustafar.spawnContents(self, "minionA", 1);
			}
			break;

		case 2:
			{
			mustafar.spawnContents(self, "minionA", 2);
			mustafar.spawnContents(self, "minionB", 1);
			}
			break;

		case 3:
			{
			mustafar.spawnContents(self, "minionB", 2);
			mustafar.spawnContents(self, "minionB", 3);
			}
			break;

		default:
			{
			debugLogging("minionWaveLaunch", "can't figure out wave number. Doing default.");
			debugLogging("//***// minionWaveLaunch: ", "////>>>> messaging ------------------------------- 'lightsCameraAction.'");
			messageTo (self, "lightsCameraAction", null, 1, false);	//bad data. Cue the next intermission.
			}
			break;

	}

	messageTo (self, "moveObiwanOuttaTheWay", null, 5, false);

	return SCRIPT_CONTINUE;
}

//----------------------------------------------------------------------
// entry from obiwan_minion_ops

messageHandler minionDied()
{
	debugLogging("minionDied", "entered");

	if(!utils.hasScriptVar(self,"minions"))
	{
		debugLogging("minionDied", "////>>>> No minions left. Messaging  'lightsCameraAction.'");
		messageTo(self, "moveBossToPostureLoc", null, 1, false);
		messageTo(self, "lightsCameraAction", null, 8, false);
		return SCRIPT_CONTINUE;
	}
	return SCRIPT_CONTINUE;
}

//----------------------------------------------------------------------
// entry from obiwan_crystal_object

messageHandler obiScoldsPlayer()		// player decides to keep the crystal
{
	obj_id player = utils.getObjIdScriptVar(self, "player");
	obj_id obiwan = utils.getObjIdScriptVar(self, "obiwan");
	obj_id crystal = utils.getObjIdScriptVar(self, "crystal");

	faceTo(obiwan, crystal);
	string_id strSpam = new string_id(mustafar.STF_OBI_MSGS, "som_obi_disappointed_cont");
	prose_package pp = prose.getPackage(strSpam);
	prose.setTT( pp, player );
	chat.chat (obiwan, player, pp);

	// drain the crystal
	doAnimationAction(obiwan, "point_forward");
	//playClientEffectObj(obiwan, "clienteffect/pl_force_absorb.cef", crystal, "");

	string effect = "clienteffect/mustafar/som_force_crystal_drain.cef";
	playClientEffectObj(player, effect, crystal, "");

	messageTo (self, "playerGetsCrystal", null, 30, false);

	return SCRIPT_CONTINUE;
}


//----------------------------------------------------------------------
// entry from this script

messageHandler playerGetsCrystal()		// player decides to keep the crystal
{
	obj_id crystal = utils.getObjIdScriptVar(self, "crystal");
	obj_id player = utils.getObjIdScriptVar(self, "player");
	obj_id obiwan = utils.getObjIdScriptVar(self, "obiwan");

	faceTo(obiwan, player);
	doAnimationAction(obiwan, "catchbreath");
	string_id strSpam = new string_id(mustafar.STF_OBI_MSGS, "som_obi_take_now");
	prose_package pp = prose.getPackage(strSpam);
	prose.setTT( pp, player );
	chat.chat (obiwan, player, pp);


	obj_id generatedItem = static_item.createNewItemFunction("item_tow_cystal_buff_drained_05_01", player);

	//drain the crystal, set objvar
	//sound and light explosion
	//doAnimationAction(player, "anims.point_forward");			// make player do animation action of an attack
	//playClientEffectObj(target, clientEffectFile, target, "");						// sound and light explosion

	string effect = "clienteffect/pl_force_healing.cef";
	playClientEffectObj(player, effect, crystal, "");

	badge.grantBadge(player, "bdg_must_obiwan_story_bad");

	destroyObject(crystal);

	messageTo (self, "obiLeaves", null, 15, false);

	return SCRIPT_CONTINUE;
}


//----------------------------------------------------------------------
// entry from obiwan_crystal_object

messageHandler obiCongratulatesPlayer()		// player destroys the crystal
{
	obj_id player = utils.getObjIdScriptVar(self, "player");
	obj_id obiwan = utils.getObjIdScriptVar(self, "obiwan");
	obj_id crystal = utils.getObjIdScriptVar(self, "crystal");
	//destroyObject(crystal);

	faceTo(obiwan, player);
	string_id strSpam = new string_id(mustafar.STF_OBI_MSGS, "som_obi_congratulates_player");
	doAnimationAction(obiwan, "nod");
	prose_package pp = prose.getPackage(strSpam);
	prose.setTT( pp, player );
	chat.chat (obiwan, player, pp);

	//generateLoot(self);
	obj_id generatedItem = static_item.createNewItemFunction("item_tow_crystal_uber_05_02", player);
	badge.grantBadge(player, "bdg_must_obiwan_story_good");

	messageTo (self, "obiLeaves", null, 5, false);

	return SCRIPT_CONTINUE;
}

//----------------------------------------------------------------------
// entry from this script

messageHandler obiLeaves()
{
	obj_id player = utils.getObjIdScriptVar(self, "player");
	obj_id obiwan = utils.getObjIdScriptVar(self, "obiwan");

	faceTo(obiwan, player);
	string_id strSpam = new string_id(mustafar.STF_OBI_MSGS, "som_obi_may_force_you");
	prose_package pp = prose.getPackage(strSpam);
	prose.setTT( pp, player );
	chat.chat (obiwan, player, pp);


	//string effect = "clienteffect/pl_force_healing.cef";
	//playClientEffectObj(self, effect, obiwan, "");
	setCreatureCover(self, 125);
	setCreatureCoverVisibility(self, false);
	playClientEffectObj(player, "clienteffect/combat_special_attacker_cover.cef", obiwan, "");

	destroyObject(obiwan);

	eventWon();

	return SCRIPT_CONTINUE;
}

//----------------------------------------------------------------------
// entry from this script

messageHandler moveBossToPostureLoc ()
{
	obj_id darkJedi = utils.getObjIdScriptVar(self, "darkJedi");
        obj_id destination = getCellId (self, "mainroom");

	setMovementRun(darkJedi);

	location here = getLocation (self);
	string planet = here.area;

	debugLogging("moveThatBossReally: ", ">>>>  planet: "+planet+" destination: "+destination);
	location home = new location (53f, 0f, 5f, planet, destination);

	ai_lib.aiPathTo (darkJedi, home);
	setHomeLocation(darkJedi, home);
	return SCRIPT_CONTINUE;
}

//----------------------------------------------------------------------
// entry from this script

messageHandler moveBossToHomeLoc ()
{
	obj_id darkJedi = utils.getObjIdScriptVar(self, "darkJedi");
        obj_id destination = getCellId (self, "mainroom");

	setMovementRun(darkJedi);

	location here = getLocation (self);
	string planet = here.area;

	debugLogging("moveThatBossReally: ", ">>>>  planet: "+planet+" destination: "+destination);
	location home = new location (31, 0f, 6f, planet, destination);

	ai_lib.aiPathTo (darkJedi, home);
	setHomeLocation(darkJedi, home);
	return SCRIPT_CONTINUE;
}


//----------------------------------------------------------------------
// entry from this script

messageHandler obiSaysDestroyCrystal()			// The Dark Jedi, having told player to get away from his crystal, runs back to his corner and summons minions
{
	obj_id player = utils.getObjIdScriptVar(self, "player");
	obj_id obiwan = utils.getObjIdScriptVar(self, "obiwan");

	faceTo(obiwan, player);
	string_id strSpam = new string_id(mustafar.STF_OBI_MSGS, "som_obi_destroy_crystal");	//obi gives player another speech about crystal here.
	prose_package pp = prose.getPackage(strSpam);
	prose.setTT( pp, player );
	chat.chat (obiwan, player, pp);

	playMusic (player, "sound/mus_mustafar_obi_wan_quest.snd");

	messageTo(self, "obiRepeatsDestroyCrystal", null, rand(20,40), false);

	return SCRIPT_CONTINUE;
}

	//----------------------------------------------------------------------
	// entry from this script

	messageHandler obiRepeatsDestroyCrystal()			// The Dark Jedi, having told player to get away from his crystal, runs back to his corner and summons minions
	{
		obj_id obiwan = utils.getObjIdScriptVar(self, "obiwan");
		if(!isIdValid(obiwan))
			return SCRIPT_CONTINUE;

		obj_id player = utils.getObjIdScriptVar(self, "player");
		if(!isIdValid(player))
			return SCRIPT_CONTINUE;

		if (!utils.hasScriptVar(player,"dealWithCrystal"))
			return SCRIPT_CONTINUE;

		if(!mustafar.stillWithinDungeonCheck (player, self))
			return SCRIPT_CONTINUE;


		faceTo(obiwan, player);
		string_id strSpam = new string_id(mustafar.STF_OBI_MSGS, "som_obi_destroy_crystal_short");	// No, seriously. Destroy the crystal. Hurry up.
		prose_package pp = prose.getPackage(strSpam);
		pp.target.set (player);
		chat.chat (obiwan, player, pp);

		messageTo(self, "obiRepeatsDestroyCrystal", null, rand(20,40), false);

		return SCRIPT_CONTINUE;
	}

//----------------------------------------------------------------------
// entry from this script

messageHandler moveMinionIntoRoom ()
{

	obj_id minion = params.getObjId("minion");
        obj_id destination = getCellId (self, "mainroom");

	setMovementRun(minion);

	location here = getLocation (self);
	string planet = here.area;

	debugLogging("moveMinionIntoRoom: ", ">>>>  planet: "+planet+" destination: "+destination);
	//location home = new location (41, 0f, 6f, planet, destination);
	location home = new location (55, 0f, 6f, planet, destination);

	location newDestination = utils.getRandomAwayLocation( home, 1f, 4f );

	ai_lib.aiPathTo (minion, newDestination);
	setHomeLocation(minion, newDestination);

	return SCRIPT_CONTINUE;
}


//----------------------------------------------------------------------
// entry from this script

messageHandler moveObiwanToPostureLocation()
{

	obj_id obiwan = utils.getObjIdScriptVar(self, "obiwan");
        obj_id destination = getCellId (self, "mainroom");

	setMovementRun(obiwan);

	location here = getLocation (self);
	string planet = here.area;

	debugLogging("moveObiwanToPostureLocation: ", ">>>>  planet: "+planet+" destination: "+destination);
	location home = new location (53.8f, -0.4f, 5.9f, planet, destination);
	//location home = new location (53, 0f, 5f, planet, destination);		// this should be sinustro's posturing location
	//location newDestination = utils.getRandomAwayLocation( home, 1f, 1f );

	ai_lib.aiPathTo (obiwan, home);
	setHomeLocation(obiwan, home);

	messageTo(obiwan, "obiwanFacePlayer", null, 7, false);

	return SCRIPT_CONTINUE;
}

//----------------------------------------------------------------------
// entry from this script

messageHandler moveObiwanOuttaTheWay()
{

	obj_id obiwan = utils.getObjIdScriptVar(self, "obiwan");
        obj_id destination = getCellId (self, "mainroom");

	setMovementRun(obiwan);

	location here = getLocation (self);
	string planet = here.area;

	debugLogging("moveObiwanOuttaTheWay: ", ">>>>  planet: "+planet+" destination: "+destination);
	location home = new location (48f, 0f, 9f, planet, destination);

	ai_lib.aiPathTo (obiwan, home);
	setHomeLocation(obiwan, home);

	messageTo(obiwan, "obiwanFacePlayer", null, 6, false);

	return SCRIPT_CONTINUE;
}

//----------------------------------------------------------------------
// entry from this script

messageHandler moveObiwanForCrystalComment ()
{

	obj_id obiwan = utils.getObjIdScriptVar(self, "obiwan");
        obj_id destination = getCellId (self, "mainroom");

	setMovementRun(obiwan);

	location here = getLocation (self);
	string planet = here.area;

	debugLogging("moveObiwanForCrystalComment: ", ">>>>  planet: "+planet+" destination: "+destination);
	location home = new location (53.8f, -0.4f, 5.9f, planet, destination);

	//location home = new location (53, 0f, 5f, planet, destination);		// this should be sinustro's posturing location
	//location newDestination = utils.getRandomAwayLocation( home, 1f, 2f );

	ai_lib.aiPathTo (obiwan, home);
	setHomeLocation(obiwan, home);

	messageTo(self, "obiSaysBeCareful2", null, 4, false);	//obi says 'gee, he's strong. Use the crystal'
	messageTo(obiwan, "obiwanFacePlayer", null, 6, false);

	return SCRIPT_CONTINUE;
}

//----------------------------------------------------------------------
// entry from this script

messageHandler moveObiwanHomeAfterCommenting()
{

	obj_id obiwan = utils.getObjIdScriptVar(self, "obiwan");
        obj_id destination = getCellId (self, "mainroom");

	setMovementRun(obiwan);

	location here = getLocation (self);
	string planet = here.area;

	debugLogging("moveObiwanHomeAfterCommenting: ", ">>>>  planet: "+planet+" destination: "+destination);
	location home = utils.getLocationScriptVar(self,"obiLocation");

	ai_lib.aiPathTo (obiwan, home);
	setHomeLocation(obiwan, home);

	messageTo(obiwan, "obiwanFacePlayer", null, 7, false);

	return SCRIPT_CONTINUE;
}

//----------------------------------------------------------------------
// entry from this script

messageHandler cleanupScriptVars ()
{

	cleanupDungeonScriptvars();

	return SCRIPT_CONTINUE;
}

//----------------------------------------------------------------------
// entry from this script

messageHandler moveObiwanToCrystalSuckLocation()
{

	obj_id obiwan = utils.getObjIdScriptVar(self, "obiwan");
        obj_id destination = getCellId (self, "mainroom");

	setMovementRun(obiwan);

	location here = getLocation (self);
	string planet = here.area;

	debugLogging("moveObiwanToCrystalSuckLocation: ", ">>>>  planet: "+planet+" destination: "+destination);
	location home = new location (60.5f, 0.1f, 15.7f, planet, destination);

	ai_lib.aiPathTo (obiwan, home);
	setHomeLocation(obiwan, home);

	messageTo (self, "obiScoldsPlayer", null, 5, false);
	messageTo(obiwan, "obiwanFaceCrystal", null, 7, false);

	return SCRIPT_CONTINUE;
}

messageHandler blowUpCrystal()
{
	obj_id crystal = utils.getObjIdScriptVar(self, "crystal");
	destroyObject(crystal);

	return SCRIPT_CONTINUE;
}

include library.ai_lib;
include library.utils;

const boolean LOGGING					= false;

trigger OnDestroy()
{
	if(!hasObjVar(self, "cleanup"))
	{
		obj_id top = getTopMostContainer (self);
		string datatable = getStringObjVar (top, "spawn_table");

		obj_id mom = getObjIdObjVar (self, "mom");
		if (mom == null)
		{
			return SCRIPT_OVERRIDE;
		}

		int spawnNum = getIntObjVar (self, "spawn_number");

		int respawnTime = dataTableGetInt (datatable, spawnNum, "respawn_time");

		if (respawnTime == 0)
		{
			respawnTime = 30;
		}

		dictionary info = new dictionary();
		info.put ("spawnNumber", spawnNum );
		info.put ("spawnMob", self );

		messageTo (mom, "tellingMomIDied", info, respawnTime, false );
	}
	
	return SCRIPT_CONTINUE;
}

trigger OnIncapacitated (obj_id killer)
{
	messageTo(self, "destroySelf", null, 5, false);
	return SCRIPT_CONTINUE;
}

trigger OnAttach()
{
	messageTo (self, "locatePatrolPoints", null, 3, false);
	return SCRIPT_CONTINUE;
}

/**
trigger OnHateTargetAdded(obj_id target)
{
	if (!isWithinHeightDifference(getLocation(self), getLocation(target)))
		removeHateTarget(self, target);
		
	return SCRIPT_CONTINUE;
}
**/

//*********************************************************************
//Functions

boolean isWithinHeightDifference(location selfLoc, location targetLoc)
{
	float heightDifference = Math.abs(targetLoc.y - selfLoc.y);
	if (heightDifference < 6)
		return true;
	else
		return false;
}

void doLogging(string section, string message)
{
	if (LOGGING)
		LOG("logging/patrol_guard/"+section, message);	
}

//************************************************************
//Message Handler

messageHandler destroySelf()
{
	destroyObject(self);
	return SCRIPT_CONTINUE;
}

messageHandler locatePatrolPoints()
{
	obj_id[] objects = utils.getSharedContainerObjects(self);
	if (objects == null || objects.length == 0)
	{
		doLogging("locatePatrolPoints", "There are no objects in the shared cell");
		return SCRIPT_CONTINUE;
	}
	
	string patrolPath = getStringObjVar(self, "patrol_path");
	string[] parse = split(patrolPath, ';');
	
	location[] patrolPoints = new location[parse.length];
	for (int i=0;i<parse.length;i++)
	{
		for (int k=0;k<objects.length;k++)
		{
			if (hasObjVar(objects[k], "patrol_wp"))
			{
				if (parse[i].equals(getStringObjVar(objects[k], "patrol_wp")))
					patrolPoints[i] = getLocation(objects[k]);
			}
		}
	}
	
	if (patrolPoints == null || patrolPoints.length == 0)
	{
		doLogging("locatePatrolPoints", "No patrol points could be found");
		return SCRIPT_CONTINUE;
	}
	
	setObjVar(self, "patrolPoints", patrolPoints);
	messageTo (self, "pathToNextPoint", null, 5, false);
	return SCRIPT_CONTINUE;
}

messageHandler pathToNextPoint()
{
	if (!hasObjVar(self, "patrolPoints"))
	{
		doLogging("pathToNextPoint", "I do not have hte patrolArray objVar");
	}
	
	location[] patrolPoints = getLocationArrayObjVar(self, "patrolPoints");
	patrol(self, patrolPoints);

	return SCRIPT_CONTINUE;
}


// Obiwan event launch instance object script - sets up and hooks up the player for the obiwan event on mustafar.
//This script borrows heavily from THicks link_player script

// ********************************************************************
//					INCLUDES
// ********************************************************************

include library.chat;
include library.mustafar;
include library.prose;
include library.sui;
include library.utils;

// ********************************************************************
//					CONSTS
// ********************************************************************

const string TRIGGER_VOLUME_CRYSTAL_STAND		= "obiwan_crystal_stand_volume";
const float OBI_INTEREST_RADIUS					= 8f;

const boolean CONST_FLAG_DO_LOGGING		= false;

// ********************************************************************
//					TRIGGERS
// ********************************************************************

trigger OnAttach ()
{
	crystalStandObjectTriggerVolumeInitializer(self);
	return SCRIPT_CONTINUE;
}

//----------------------------------------------------------------------

trigger OnInitialize ()
{
	crystalStandObjectTriggerVolumeInitializer(self);
	return SCRIPT_CONTINUE;
}

//----------------------------------------------------------------------

trigger OnTriggerVolumeEntered(string volumeName, obj_id breacher)
{
	if(!utils.hasScriptVar(self,"haveSpawnedObi"))
	{
		if (isPlayer(breacher) && !isIncapacitated(breacher))
		{
			if (volumeName.equals(TRIGGER_VOLUME_CRYSTAL_STAND))
			{
				//if (mustafar.canCallObiwan(breacher, self))				// if we got into the dungeon, we should just assume we're gonna call obiwan
				//{
					debugLogging("//***// OnTriggerVolumeEntered: ", "////>>>> we CAN call obiwan.");

					obj_id dungeon = utils.getObjIdScriptVar(self,"dungeon");
					if(!isIdValid(dungeon))
					{
						debugLogging("//***// OnTriggerVolumeEntered: ", "////>>>> dungeon obj_id scriptVar invalid. EXITING.");
						return SCRIPT_CONTINUE;
					}
					debugLogging("//***// crystalStandObjectTriggerVolumeInitializer: ", "////>>>> going to spawn obi!");
					spawnObi(breacher, dungeon, self);
				//}
				//else
				//	debugLogging("//***// OnTriggerVolumeEntered: ", "////>>>> we CAN'T call obiwan.");
			}
		}
	}	

	return SCRIPT_CONTINUE;
}

// ********************************************************************
//					FUNCTIONS
// ********************************************************************

void debugLogging(string section, string message)
{
	if (CONST_FLAG_DO_LOGGING)
		LOG("debug/obiwan_jedi_crystal_stand/"+section, message);
}


//----------------------------------------------------------------------

void crystalStandObjectTriggerVolumeInitializer(obj_id self)
{
	obj_id dungeon = getTopMostContainer(self);
	if (!isIdValid(dungeon))
	{
		debugLogging("//***// crystalStandObjectTriggerVolumeInitializer: ", "////>>>> dungeon obj_id is invalid. BROKEN");
		return;
	}

	if(!utils.hasScriptVar(dungeon,"obiwan") && !utils.hasScriptVar(self,"haveSpawnedObi"))
	{
		if(!hasTriggerVolume(self, TRIGGER_VOLUME_CRYSTAL_STAND))
		{
			debugLogging("//***// crystalStandObjectTriggerVolumeInitializer: ", "////>>>> created new trigger volume TRIGGER_VOLUME_CRYSTAL_STAND");
			createTriggerVolume(TRIGGER_VOLUME_CRYSTAL_STAND, OBI_INTEREST_RADIUS, true);
		}
		else
		{
			obj_id[] denizens = getTriggerVolumeContents(self, TRIGGER_VOLUME_CRYSTAL_STAND);
			debugLogging("//***// crystalStandObjectTriggerVolumeInitializer: ", "////>>>> got trigger volume contents. There are: "+denizens.length+" things in the trigger volume");
			for(int i = 0; i < denizens.length; i++)
			{
				if(isPlayer(denizens[i]) && !isIncapacitated(denizens[i]))
				{
					debugLogging("//***// crystalStandObjectTriggerVolumeInitializer: ", "////>>>> Found a player who also isn't incapped. Going to spawn Obi.");			
					spawnObi(denizens[i], dungeon, self);
					return;
				}
			}
		}
	}
	return;
}

//----------------------------------------------------------------------

void spawnObi(obj_id player, obj_id dungeon, obj_id landmark)
{
	debugLogging("//***// spawnObi: ", "////>>>> entered.");
	obj_id obiwan = mustafar.callObiwan(player, landmark);
	
	if (isIdValid(obiwan))
	{
		utils.setScriptVar(dungeon,"obiLocation",getLocation(obiwan));
		clearCondition (obiwan, CONDITION_CONVERSABLE);
		detachScript(obiwan,"conversation.som_kenobi_obi_wan");
	}	
		
	
	//setup event actor id's on the dungeon
	utils.setScriptVar(dungeon,"player", player);
	utils.setScriptVar(dungeon,"obiwan", obiwan);
	utils.setScriptVar(landmark,"haveSpawnedObi",1);
	
	messageTo(dungeon, "obiwanSpeechDelay", null, 5, false);	//This messagehandler sets up the delay between Obi spawning at the crystal, and his speech
}

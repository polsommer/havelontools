include ai.ai;
include library.ai_lib;
include library.utils;
include library.create;
include library.trial;
include library.space_dungeon;
include library.instance;

const string SPAWNED_LIST			= "spawned";
const string SPAWNED				= SPAWNED_LIST+"."+"spawnNumber_";
const string GENERIC_SPAWNER_ACTIVE		= "genericSpawner.isActive";

trigger OnAttach ()
{
	string name = getTemplateName (self);
	if (name == "object/building/general/bunker_allum_mine.iff")
	{
		string setting = getConfigSetting("Dungeon", "Death_Watch");
		if ( setting == null || setting == "false" || setting == "0")
		{
			return SCRIPT_CONTINUE;
		}
	}

	if (!canSpawnByConfigSetting())
		return SCRIPT_CONTINUE;

	// We only want a space dungeon to spawn when someone is sent there rather than automatically.
	if ( !hasObjVar(self, "space_dungeon.name") && !hasScript(self, "systems.instance.instance_manager"))
		messageTo ( self, "beginSpawn", null, 1, false );

	return SCRIPT_CONTINUE;
}

trigger OnInitialize ()
{
	string name = getTemplateName (self);
	if (name == "object/building/general/bunker_allum_mine.iff")
	{
		string setting = getConfigSetting("Dungeon", "Death_Watch");
		if ( setting == null || setting == "false" || setting == "0")
		{
			return SCRIPT_CONTINUE;
		}
	}

	if (!canSpawnByConfigSetting())
		return SCRIPT_CONTINUE;

	// We only want a space dungeon to spawn when someone is sent there rather than automatically.
	if ( !hasObjVar(self, "space_dungeon.name")  && !hasScript(self, "systems.instance.instance_manager"))
		messageTo ( self, "beginSpawn", null, 1, false );

	return SCRIPT_CONTINUE;
}

messageHandler beginSpawn ()
{
	string datatable = getStringObjVar (self, "spawn_table");

	if (datatable == null || datatable == "")
	{
		return SCRIPT_OVERRIDE;
	}

	int numberOfCreaturesToSpawn = dataTableGetNumRows (datatable);
	int x = utils.getIntScriptVar (self, "spawnCounter");
	setDungeonActive(self, true);
	while (x < numberOfCreaturesToSpawn)
	{
		if (hasObjVar (self, SPAWNED + x))
		{
			obj_id check = getObjIdObjVar (self, SPAWNED + x);
			if (!check.isLoaded())
			{
				spawnCreatures(x, datatable, self );
			}
		}
		else
		{
			spawnCreatures(x, datatable, self );
		}
		x = x + 1;
	}

	if (dataTableHasColumn (datatable, "special_room"))
	{
		attachRoomScripts (self, datatable);
	}

	if (dataTableHasColumn (datatable, "room_objvar"))
	{
		setRoomObjVars (self, datatable);
	}

	return SCRIPT_CONTINUE;
}

// To make compatable with the instance_manager
messageHandler cleanupSpawn()
{
	messageTo(self, "dungeonCleanup", null, 0.0f, false);
	return SCRIPT_CONTINUE;
}

messageHandler dungeonCleanup()
{
	setDungeonActive(self, false);
	
	obj_var_list ovl = getObjVarList(self, SPAWNED_LIST);
	if ( (ovl != null) && (ovl.getNumItems() != 0) )
	{
		int numItems = ovl.getNumItems();
		for ( int i = 0; i < numItems; i++ )
		{
			removeObjVar(self, SPAWNED+i);
		}
	}

	
	obj_id[] objects = trial.getAllObjectsInDungeon(self);
	
	if (objects == null || objects.length == 0)
		return SCRIPT_CONTINUE;
		
	
	for (int i= 0;i<objects.length;i++)
	{
		if (isPlayer(objects[i]))
		{
			if (instance.isInInstanceArea(objects[i]))
			{
				instance.requestExitPlayer(self, objects[i], 1);
				continue;
			}
			else 
			{
				space_dungeon.verifyPlayerSession(objects[i]);
				continue;
			}
		}
		
		if (isMob(objects[i]))
		{
			trial.cleanupNpc(objects[i]);
			continue;
		}	
		
		
		if (trial.isTempObject(objects[i]))
		{
			trial.cleanupNpc(objects[i]);// yeah yeah, it works on objects too
			continue;
		}
	}

	return SCRIPT_CONTINUE;
}

messageHandler tellingMomIDied()
{
	if (!isDungeonActive(self))
		return SCRIPT_CONTINUE;
		
	int spawn_num = params.getInt ("spawnNumber");
	obj_id spawn_mob = params.getObjId ("spawnMob");

	// only spawn a replacement if the mob who died is the one we actually spawned
	if (hasObjVar (self, SPAWNED + spawn_num) && (spawn_mob == getObjIdObjVar (self, SPAWNED + spawn_num)))
	{
		string datatable = getStringObjVar (self, "spawn_table");
		spawnCreatures(spawn_num, datatable, self );
	}

	return SCRIPT_CONTINUE;
}

boolean isDungeonActive(obj_id dungeon)
{
 	if (utils.hasScriptVar(dungeon, GENERIC_SPAWNER_ACTIVE))
 		return utils.getBooleanScriptVar(dungeon, GENERIC_SPAWNER_ACTIVE);
 	else
		return true;
}

void setDungeonActive(obj_id dungeon, boolean state)
{
	utils.setScriptVar(dungeon, GENERIC_SPAWNER_ACTIVE, state);
}

void attachRoomScripts (obj_id self, string datatable)
{
	string [] roomsToLock = dataTableGetStringColumnNoDefaults (datatable, "special_room");
	int numRooms = roomsToLock.length;

	int passThrough = 0;

	while (passThrough < numRooms)
	{
		string roomName = dataTableGetString (datatable, passThrough, "special_room");
		string roomScript = dataTableGetString (datatable, passThrough, "special_room_script");

		if (roomName == null && roomName == "")
		{
			setObjVar (self, "problem", "No room name");
			return;
		}

		if (roomScript == null && roomScript == "")
		{
			setObjVar (self, "problem", "No Script");
			return;
		}

		obj_id roomObj = self;

		if (roomName == "self")
		{
			roomObj = self;
		}
		else
		{
			roomObj = getCellId (self, roomName);
		}

		attachScript (roomObj, roomScript);
		setObjVar (self, "set_room", passThrough);
		passThrough = passThrough + 1;
	}
	return;
}

void setRoomObjVars (obj_id self, string datatable)
{
	string [] roomsToSet = dataTableGetStringColumnNoDefaults (datatable, "room_objvar");
	int numRooms = roomsToSet.length;

	if (numRooms == 0)
	{
		return;
	}

	int passThrough = 0;

	while (passThrough < numRooms)
	{
		string roomName = dataTableGetString (datatable, passThrough, "room_objvar");
		string roomObjVar = dataTableGetString (datatable, passThrough, "room_objvar_name");
		string roomObjVarValue = dataTableGetString (datatable, passThrough, "room_objvar_value");

		if (roomName == null && roomName == "")
		{
			setObjVar (self, "problem", "No room name");
			return;
		}

		if (roomObjVar == null && roomObjVar == "")
		{
			setObjVar (self, "problem", "No ObjVar Name");
			return;
		}

		obj_id roomObj = getCellId (self, roomName);

		setObjVar (roomObj, roomObjVar, roomObjVarValue);

		passThrough = passThrough + 1;
	}
	return;
}

void spawnCreatures(int x, string datatable, obj_id self )
{
	string spawn = dataTableGetString (datatable, x, "spawns");
	string creatureName = dataTableGetString (datatable, x, "name");

	float xCoord = dataTableGetFloat (datatable, x, "loc_x");
	float yCoord = dataTableGetFloat (datatable, x, "loc_y");
	float zCoord = dataTableGetFloat (datatable, x, "loc_z");

	location myself = getLocation (self);

	string planet = myself.area;
	string spawnRoom = dataTableGetString (datatable, x, "room");
	obj_id room = getCellId (self, spawnRoom);

	if (!isIdValid (room) && (spawnRoom != "world"))
	{
		CustomerServiceLog("bad_spawner_data", "spawner " + self + ", datatable " + datatable + ", row " + x + ", mob " + spawn + ", room " + spawnRoom + " doesn't exist");
		removeObjVar (self, SPAWNED + x);

		return;
	}

	location spawnPoint = new location (xCoord, yCoord, zCoord, planet, room);

	//Let's see if there's a boss mob that needs to spawn...
	if (dataTableHasColumn (datatable, "boss") && dataTableHasColumn (datatable, "boss_chance") && dataTableHasColumn (datatable, "boss_name"))
	{
		string boss = dataTableGetString (datatable, x, "boss");
		int percentage = dataTableGetInt (datatable, x, "boss_chance");
		string bossName = dataTableGetString (datatable, x, "boss_name");

		if (boss != null && boss != "none" && boss != "")
		{
			int bossRoll = rand (1, 10000);
			if (percentage != 0)
			{
				if (bossRoll < percentage)
				{
					spawn = boss;
					if (bossName != null && bossName != "")
					{
						creatureName = bossName;
					}
					else
					{
						creatureName = "";
					}
				}
			}
		}
	}
	//Boss Mob Check is done, and if he's there our spawn string has been updated

	obj_id spawnedCreature = create.object (spawn, spawnPoint);
	if (!isIdValid(spawnedCreature))
	{
		CustomerServiceLog("bad_spawner_data", "spawner " + self + ", datatable " + datatable + ", row " + x + ", mob " + spawn + " doesn't exist");
		removeObjVar (self, SPAWNED + x);
		return;
	}

	if (dataTableHasColumn (datatable, "convo_script"))
	{
		string convoScript = dataTableGetString (datatable, x, "convo_script");
		if (convoScript != null && convoScript != "")
		{
			attachScript (spawnedCreature, convoScript);
		}
	}

	if (creatureName != null && creatureName != "")
	{
		setName (spawnedCreature, creatureName);
	}

	setObjVar (spawnedCreature, "spawn_number", x);
	setObjVar (spawnedCreature, "mom", self );
	setObjVar (self, SPAWNED + x, spawnedCreature);
	trial.markAsTempObject(spawnedCreature, false);

	if (dataTableHasColumn (datatable, "script"))
	{
		string scriptList = dataTableGetString (datatable, x, "script");

		if (scriptList != null && scriptList != "")
		{
			string[] scriptArray = split(scriptList, ',' );

			for ( int i = 0; i < scriptArray.length; i++ )
			{
				attachScript( spawnedCreature, scriptArray[i] );
			}

		}
	}

	if (dataTableHasColumn (datatable, "yaw"))
	{
		float spawnYaw = dataTableGetFloat (datatable, x, "yaw");
		if (spawnYaw != 0)
		{
			setYaw (spawnedCreature, spawnYaw);
		}
	}

	if (dataTableHasColumn (datatable, "spawn_objvar2"))
	{

		string spawnObjVar = dataTableGetString (datatable, x, "spawn_objvar");
		string objvarValue1 = dataTableGetString (datatable, x, "spawn_objvar_value");
		string spawnObjVar2 = dataTableGetString (datatable, x, "spawn_objvar2");
		string spawnValue2 = dataTableGetString (datatable, x, "spawn_objvar_value2");

		if (spawnObjVar != null && spawnObjVar != "")
		{
			if (objvarValue1 != null && objvarValue1 != "")
			{
				setObjVar (spawnedCreature, spawnObjVar, objvarValue1);
			}
		}
		if (spawnObjVar2 != null && spawnObjVar2 != "")
		{
			if (spawnValue2 != null && spawnValue2 != "")
			{
				setObjVar (spawnedCreature, spawnObjVar2, spawnValue2);
			}
		}
	}
	else
	{
		if (dataTableHasColumn (datatable, "spawn_objvar"))
		{
			string objVarList = dataTableGetString (datatable, x, "spawn_objvar");
			if (objVarList != null && objVarList != "")
			{
				setCreatureObjVars ( spawnedCreature, objVarList );
			}
		}
	}

	if (dataTableHasColumn (datatable, "animation_mood"))
	{
		dictionary outparams = new dictionary ();
		outparams.put ("x", x );
		outparams.put ("datatable", datatable );
		outparams.put ("spawnedCreature", spawnedCreature );
		messageTo ( self, "animationMood", outparams, 3, false );
	}

	if (dataTableHasColumn (datatable, "mood"))
	{
		string creatureMood = dataTableGetString (datatable, x, "mood");
		if (creatureMood != null && creatureMood != "")
		{
			ai_lib.setDefaultCalmMood (spawnedCreature, creatureMood);
		}
	}

	return;
}

messageHandler animationMood ()
{
	int x = params.getInt( "x" );
	string datatable = params.getString( "datatable" );
	obj_id spawnedCreature = params.getObjId( "spawnedCreature" );
	string creatureAnimationMood = dataTableGetString (datatable, x, "animation_mood");

	if (creatureAnimationMood != null && creatureAnimationMood != "")
	{
		ai_lib.setAnimationMood (spawnedCreature, creatureAnimationMood);
	}
	return SCRIPT_CONTINUE;
}

void setCreatureObjVars( obj_id creature, string objVarList )
{
	if ( objVarList == null || objVarList == "" )
		return;

	//get array of strings in the format int:name=value
	string[] pairs = split(objVarList, ',');
	for ( int i = 0; i < pairs.length; i++ )
	{
		//split each pair to get int:name and value
		string[] objVarToSet = split(pairs[i], '=' );
		string objVarValue = objVarToSet[1];
		//split int:name into type (int) and name ("name")
		string[] objVarNameAndType = split(objVarToSet[0], ':');
		string objVarType = objVarNameAndType[0];
		string objVarName = objVarNameAndType[1];

		//Now we have:
		// objVarType	=	string, int, float, boolean
		// objVarName	=	the name of the objvar to set
		// objVarValue	=	the value of the objvar

		if ( objVarType == "string" )
			setObjVar( creature, objVarName, objVarValue );
		else if ( objVarType == "int" )
			setObjVar( creature, objVarName, utils.stringToInt( objVarValue ) );
		else if ( objVarType == "float" )
			setObjVar( creature, objVarName, utils.stringToFloat( objVarValue ) );
		else if ( objVarType == "boolean" || objVarType == "bool" )
			setObjVar( creature, objVarName, utils.stringToInt( objVarValue ) );//booleans are stored as int's.
		else
			setObjVar( creature, objVarName, objVarValue );//fuck it, must have been a string after all.
	}
}

boolean canSpawnByConfigSetting()
{
	string disableSpawners = getConfigSetting("GameServer", "disableGenericSpawner");
	if (disableSpawners == null)
		return true;

	if (disableSpawners.equals("true") || disableSpawners.equals("1"))
		return false;

	return true;
}

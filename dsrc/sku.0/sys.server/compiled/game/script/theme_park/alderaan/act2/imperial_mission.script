include library.utils;
include library.create;

const string IMPERIAL_STF			= "theme_park/alderaan/act2/imperial_missions";
const string IMPERIAL_SHARED_STF	= "theme_park/alderaan/act2/shared_imperial_missions";


trigger OnAttach()
{
	if (hasScript(self, "theme_park.alderaan.act2.rebel_mission"))
		detachScript(self, "theme_park.alderaan.act2.rebel_mission");

	startImperialMission(self);
	
	return SCRIPT_CONTINUE;
}	

trigger OnDetach()
{
	if (hasObjVar(self, "coa2.imperial"))
	{
		int missionNum = getIntObjVar(self, "coa2.imperial.missionNum");
		
		abortImperialMission(self, missionNum, false);
	}

	return SCRIPT_CONTINUE;
}

trigger OnArrivedAtLocation(string name)
{
	int missionNum = getIntObjVar(self, "coa2.imperial.missionNum");
	
	if (name == "missionSpawner")
	{
		//debugSpeakMsg (self, "Arrived at missionSpawner trigger");

		location missionLoc = getLocationObjVar(self, "coa2.imperial.missionLoc");
		string missionNpc = getStringObjVar(self, "coa2.imperial.missionNpc");

		if (missionNpc != null && !missionNpc.equals(""))
		{
			obj_id npc = create.object(missionNpc, missionLoc);
			setObjVar(self, "coa2.imperial.missionNpcId", npc);
			setObjVar(npc, "coa2.imperial.playerId", self);
		}
		else
			removeObjVar(self, "coa2.imperial.missionNpcId");
	}
	
	if (name == "missionDest")
	{
		setObjVar(self, "coa2.imperial.success", 1);

		//debugSpeakMsg (self, "Arrived at missionDest trigger");

		if (hasObjVar(self, "coa2.imperial.missionNpcId"))
		{
			obj_id npc = getObjIdObjVar(self, "coa2.imperial.missionNpcId");

			if (!isIdValid(npc))
			{
				string_id error = new string_id (IMPERIAL_SHARED_STF, "m" + missionNum + "_init_failure");
				sendSystemMessage(self, error);
				abortImperialMission(self, missionNum, true);
			}
			else
			{
				removeMissionWaypoint(self);
			}
		}
		else
		{
			removeMissionWaypoint(self);
		}
	}
	
	if (name == "returnDest")
	{
		setObjVar(self, "coa2.imperial.success", 1);

		//debugSpeakMsg (self, "Arrived at returnDest trigger");

		removeMissionWaypoint(self);
		removeLocationTarget(name);
	}
	
	return SCRIPT_CONTINUE;
}

messageHandler handleRestartMission()
{
	setObjVar(self, "coa2.imperial.success", 1);
	removeMissionWaypoint(self);	
	startImperialMission(self);
	
	return SCRIPT_CONTINUE;
}

messageHandler handleAbortMission()
{
	obj_id player = params.getObjId("player");
	int missionNum = params.getInt("value");
	
	abortImperialMission(player, missionNum, false);
	
	return SCRIPT_CONTINUE;
}

messageHandler createReturnMission()
{
	if (hasObjVar(self, "coa2.imperial.waypoint"))
		removeMissionWaypoint(self);

	int missionNum = getIntObjVar(self, "coa2.imperial.missionNum");
	location returnLoc = getLocationObjVar(self, "coa2.imperial.returnLoc");

	string_id waypointName = new string_id(IMPERIAL_SHARED_STF, "waypoint_return_name_" + missionNum);
	string_id waypointDesc = new string_id(IMPERIAL_SHARED_STF, "waypoint_return_desc_" + missionNum);

	obj_id waypoint = createMissionWaypoint(self, returnLoc, 20, getString(waypointName), "returnDest");

	if (isIdValid(waypoint))
	{
		setObjVar(self, "coa2.imperial.waypointDesc", waypointDesc);
	}
	
	return SCRIPT_CONTINUE;
}

void startImperialMission(obj_id player)
{
	int missionNum = getIntObjVar(player, "coa2.imperial.missionNum");
	location missionLoc = getLocationObjVar(player, "coa2.imperial.missionLoc");

	string_id waypointName = new string_id (IMPERIAL_SHARED_STF, "waypoint_name_" + missionNum);
	string_id waypointDesc = new string_id (IMPERIAL_SHARED_STF, "waypoint_desc_" + missionNum);
		
	obj_id waypoint = createMissionWaypoint(player, missionLoc, 20, getString(waypointName), "missionDest");

	//debugSpeakMsg (player, "Created new Waypoint("+waypoint+") to Location("+missionLoc.toString()+")");

	if (isIdValid(waypoint))
	{
		setObjVar(player, "coa2.imperial.waypointDesc", waypointDesc);
		addLocationTarget ("missionSpawner", missionLoc, 300);
	}
	else
	{
		string_id error = new string_id (IMPERIAL_SHARED_STF, "waypoint_failure");
		sendSystemMessage(player, error);
	}
}

void abortImperialMission(obj_id player, int mission, boolean returnHome)
{
	removeMissionWaypoint(player);

	if (returnHome)
	{
		messageTo(player, "createReturnMission", null, 1, false);
	}
	else
	{
		revertMissionProgress(player, mission);
		
		removeObjVar(player, "coa2.imperial");	
		detachScript(player, "theme_park.alderaan.act2.imperial_mission");
	}
}

void revertMissionProgress(obj_id player, int mission)
{
	switch (mission)
	{
		case 1:
			setObjVar(player, "coa2.progress", 0);
			break;

		case 2:
			setObjVar(player, "coa2.progress", -3);
			break;

		case 3:
			setObjVar(player, "coa2.progress", -3);
			break;

		case 4:
			setObjVar(player, "coa2.progress", -6);
			break;

		case 5:
			setObjVar(player, "coa2.progress", -8);
			break;
	}

}

obj_id createMissionWaypoint(obj_id player, location loc, float radius, string waypointName, string volumeName)
{
	addLocationTarget(volumeName, loc, radius);
	obj_id waypoint = createWaypointInDatapad(player, loc);

	if (isIdValid(waypoint))
	{
		setWaypointActive(waypoint, true);
		setWaypointName (waypoint, waypointName);

		setObjVar(player, "coa2.imperial.waypoint", waypoint);
		setObjVar(player, "coa2.imperial.volume", volumeName);
	}

	return waypoint;
}

void removeMissionWaypoint(obj_id player)
{
	obj_id waypoint = getObjIdObjVar(player, "coa2.imperial.waypoint");

	//debugSpeakMsg (player, "Destroying Waypoint - " + waypoint);

	if (hasObjVar(player, "coa2.imperial.volume"))
	{
		string name = getStringObjVar(player, "coa2.imperial.volume");
		removeLocationTarget(name);

		if (name.equals("missionDest"))
			removeLocationTarget("missionSpawner");
		
		removeObjVar(player, "coa2.imperial.volume");
	}
	
	setWaypointVisible(waypoint, false);
	setWaypointActive(waypoint, false);
	destroyWaypointInDatapad(waypoint, player);
	removeObjVar(player, "coa2.imperial.waypoint");
}

trigger OnWaypointGetAttributes (obj_id waypoint, obj_id player, string[] names, string[] attribs)
{
	obj_id wp = getObjIdObjVar(player, "coa2.imperial.waypoint");
	
	if (waypoint != wp)
	{
		return SCRIPT_CONTINUE;
	}

	int idx = utils.getValidAttributeIndex(names);
	if (idx == -1) 
		return SCRIPT_CONTINUE;

	string_id waypointDesc = getStringIdObjVar(player, "coa2.imperial.waypointDesc");

	names[idx] = "mission_details";
	attribs[idx] = "@"+waypointDesc.toString();
	
	return SCRIPT_CONTINUE;
}	

trigger OnWaypointDestroyed(obj_id waypoint)
{
	obj_id wp = getObjIdObjVar(self, "coa2.imperial.waypoint");
	
	if (waypoint != wp)
	{
		return SCRIPT_CONTINUE;
	}

	//debugSpeakMsg (self, "Destroying Waypoint - " + waypoint);

	if (hasObjVar(self, "coa2.imperial.success"))
	{
		removeObjVar(self, "coa2.imperial.success");
	}
	else
	{
		int missionNum = getIntObjVar(self, "coa2.imperial.missionNum");
		revertMissionProgress(self, missionNum);

		debugSpeakMsg (self, "Reverting Mission Progress - " + missionNum);
	}

	removeObjVar(self, "coa2.imperial.waypoint");
	removeObjVar(self, "coa2.imperial.volume");

	return SCRIPT_CONTINUE;
}	

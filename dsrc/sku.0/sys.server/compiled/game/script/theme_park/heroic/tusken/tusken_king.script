include library.ai_lib;
include library.buff;
include library.factions;
include library.utils;

messageHandler setupSquad()
{
	messageTo(self, "establishUnity", null, 0.0f, false);	
	return SCRIPT_CONTINUE;
}

trigger OnEnteredCombat()
{
	messageTo(self, "establishUnity", null, 0.0f, false);	
	return SCRIPT_CONTINUE;
}

messageHandler establishUnity()
{
	obj_id[] allObj = getNPCsInRange(getLocation(self), 150.0f);//utils.getAllObjectsInBuildoutArea(self);
	
	resizeable obj_id[] allies = new obj_id[0];
		
	for (int i=0;i<allObj.length;i++)
	{
		if (isDead(allObj[i]))
			continue;
		
		if (factions.getFaction(allObj[i]) == null || factions.getFaction(allObj[i]) == "")
			continue;
			
		if (!factions.getFaction(allObj[i]).equals("heroic_tusken"))
			continue;
			
		if (!isIdValid(getLocation(self).cell) && isIdValid(getLocation(allObj[i]).cell))
			continue;
			
		if (isIdValid(getLocation(self).cell) && isIdValid(getLocation(allObj[i]).cell) && getLocation(self).cell != getLocation(allObj[i]).cell)
			continue;
			
		allies.add(allObj[i]);		
	}
	
	ai_lib.establishSharedHealth(allies);
	obj_id[] someList = allies;
	ai_lib.establishAgroLink(self, someList);
	buff.applyBuff(someList, "tusken_unity");
	return SCRIPT_CONTINUE;
}
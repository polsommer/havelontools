include library.ai_lib;
include library.create;
include library.spawning;
include library.utils;

const string NYM_OBJECT_DATATABLE	= "datatables/spawning/theme_park/nym_research_lab_objects.iff";
const string SPAWNER_DATATABLE		= "datatables/spawning/theme_park/nym_research_lab.iff";

trigger OnInitialize ()
{
	messageTo(self, "spawnQuestObjects", null, 3, false);
	messageTo(self, "spawnOtherObjects", null, 3, false);
	messageTo(self, "beginSpawn", null, 3, false);	
	return SCRIPT_CONTINUE;
}

messageHandler spawnQuestObjects()
{
	if(!isValidId(self) || !exists(self))
	{
		return SCRIPT_CONTINUE;
	}
	
	//New datatable object spawn functionality
	if(!utils.hasScriptVar(self, "planet"))
	{
		location myself = getLocation(self);
		string planet = myself.area;
		if(planet == null || planet.length() <= 0)
		{
			CustomerServiceLog("bad_spawner_data", "Nym Sulfur Pirate Cave spawner " + self + ", Could not retrieve planet name from location.");
			return SCRIPT_CONTINUE;
		}
		utils.setScriptVar(self, "planet", planet);

	}
	
	string planet = utils.getStringScriptVar(self, "planet");
	if(planet == null || planet.length() <= 0)
	{
		CustomerServiceLog("bad_spawner_data", "Nym Sulfur Pirate Cave spawner " + self + ", Could not retrieve planet name script var.");
		return SCRIPT_CONTINUE;
	}

	spawning.spawnObjectsInDungeonFromTable(self, planet, NYM_OBJECT_DATATABLE);
	return SCRIPT_CONTINUE;
}

messageHandler beginSpawn ()
{
	if(!isValidId(self) || !exists(self))
	{
		return SCRIPT_CONTINUE;
	}

	int numberOfCreaturesToSpawn = (dataTableGetNumRows (SPAWNER_DATATABLE) - 1);
	if(numberOfCreaturesToSpawn < 0)
	{
		CustomerServiceLog("bad_spawner_data", "Nym Research Lab spawner " + self + ", research_lab.beginSpawn handler could not open datatable.");	
		return SCRIPT_CONTINUE;
	}
	int x = 0;
	if(utils.hasScriptVar(self, "spawnCounter"))
		x = utils.getIntScriptVar (self, "spawnCounter");

	string spawn = dataTableGetString (SPAWNER_DATATABLE, x, "spawns");

	float xCoord = dataTableGetFloat (SPAWNER_DATATABLE, x, "loc_x");
	float yCoord = dataTableGetFloat (SPAWNER_DATATABLE, x, "loc_y");
	float zCoord = dataTableGetFloat (SPAWNER_DATATABLE, x, "loc_z");

	location myself = getLocation (self);
	string planet = myself.area;

	string spawnRoom = dataTableGetString (SPAWNER_DATATABLE, x, "room");
	obj_id room = null;

	if ( spawnRoom != null && spawnRoom.length() > 0 )
	{
		room = getCellId (self, spawnRoom);
	}
	else
	{
		spawnRoom = "";
	}

	if ( !isIdValid (room) && !spawnRoom.equals("world") )
	{
		CustomerServiceLog("bad_spawner_data", "spawner " + self + ", datatable " + SPAWNER_DATATABLE + ", row " + x + ", mob " + spawn + ", room " + spawnRoom + " doesn't exist");
	}
	else
	{
		location spawnPoint = new location (xCoord, yCoord, zCoord, planet, room);

		obj_id spawnedCreature = create.object (spawn, spawnPoint);

		string geoScript = dataTableGetString (SPAWNER_DATATABLE, x, "script");

		if (geoScript != null && geoScript != "")
		{

			attachScript (spawnedCreature, geoScript);
		}
		string objVars = dataTableGetString (SPAWNER_DATATABLE, x, "objvar");
		if (objVars != null && objVars.length() > 0)
		{
			utils.setObjVarsListUsingSemiColon(spawnedCreature, objVars);
		}

		string creatureName = dataTableGetString (SPAWNER_DATATABLE, x, "name");

		if (creatureName != null && creatureName != "")
		{
			setName (spawnedCreature, creatureName);
		}

		setObjVar (spawnedCreature, "spawn_number", x);
		setObjVar (spawnedCreature, "mom", self );
		utils.setScriptVar (self, "spawned" + x, spawnedCreature);
	}

	x = x + 1;
	utils.setScriptVar (self, "spawnCounter", x);

	if (x <= numberOfCreaturesToSpawn)
	{
		messageTo ( self, "beginSpawn", null, 1, false );
	}

	return SCRIPT_CONTINUE;
}


messageHandler tellingMomIDied()
{
	//debugSpeakMsg (self, "Going with the Spawner");
	int spawn_num = params.getInt ("spawnNumber");
	obj_id spawn_mob = params.getObjId ("spawnMob");

	// only spawn a replacement if the mob who died is the one we actually spawned
	if (utils.hasScriptVar (self, "spawned" + spawn_num) && (spawn_mob == utils.getObjIdScriptVar (self, "spawned" + spawn_num)))
	{
		doSpawn (spawn_num);
	}

	return SCRIPT_CONTINUE;
}

void doSpawn(int spawn_num)
{
	obj_id self = getSelf();
	//debugSpeakMsg (self, "Do SPawn going off");
	int numberOfCreaturesToSpawn = dataTableGetNumRows (SPAWNER_DATATABLE);
	if(numberOfCreaturesToSpawn < 0)
	{
		CustomerServiceLog("bad_spawner_data", "Nym Research Lab spawner " + self + ", research_lab.beginSpawn handler could not open datatable.");	
		return;
	}
	int x = spawn_num;

	string spawn = dataTableGetString (SPAWNER_DATATABLE, x, "spawns");

	float xCoord = dataTableGetFloat (SPAWNER_DATATABLE, x, "loc_x");
	float yCoord = dataTableGetFloat (SPAWNER_DATATABLE, x, "loc_y");
	float zCoord = dataTableGetFloat (SPAWNER_DATATABLE, x, "loc_z");

	location myself = getLocation (self);
	string planet = myself.area;

	string spawnRoom = dataTableGetString (SPAWNER_DATATABLE, x, "room");
	obj_id room = getCellId (self, spawnRoom);

	if ( !isIdValid (room) && !spawnRoom.equals("world") )
	{
		CustomerServiceLog("bad_spawner_data", "spawner " + self + ", datatable " + SPAWNER_DATATABLE + ", row " + x + ", mob " + spawn + ", room " + spawnRoom + " doesn't exist");
		utils.removeScriptVar (self, "spawned" + x);
		return;
	}

	location spawnPoint = new location (xCoord, yCoord, zCoord, planet, room);

	obj_id spawnedCreature = create.object (spawn, spawnPoint);

	string geoScript = dataTableGetString (SPAWNER_DATATABLE, x, "script");

	if (geoScript != null && geoScript != "")
	{

		attachScript (spawnedCreature, geoScript);
	}

	string creatureName = dataTableGetString (SPAWNER_DATATABLE, x, "name");

	if (creatureName != null && creatureName != "")
	{
		setName (spawnedCreature, creatureName);
	}

	setObjVar (spawnedCreature, "spawn_number", x);
	setObjVar (spawnedCreature, "mom", self );
	utils.setScriptVar (self, "spawned" + x, spawnedCreature);

	//debugSpeakMsg (spawnedCreature, "Respawned by Mom (" + self + ") I'm spawn Number " + x);

	return;
}


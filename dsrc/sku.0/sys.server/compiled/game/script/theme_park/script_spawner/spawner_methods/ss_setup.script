// Behavior handler attached to NPCs that are spawned by a Travis spawner.

include library.ai_lib;

//------------------------------------------------
// OnAttach
//------------------------------------------------

trigger OnAttach()
{
	messageTo ( self, "beginSetup", null, 1, false );
	return SCRIPT_CONTINUE;
}

//------------------------------------------------
// beginSetup
//------------------------------------------------

messageHandler beginSetup()
{
	// Execute spawn commands.
	if ( hasObjVar(self, "spawn_objvar") )
	{
		string objVarOne = getStringObjVar( self, "spawn_objvar" );
		setObjVarScript( self,objVarOne );
	}

	if( hasObjVar(self, "spawn_objvar2") )
	{
		string objVarTwo = getStringObjVar( self, "spawn_objvar2" );
		setObjVarScript( self,objVarTwo );
	}

	return SCRIPT_CONTINUE;
}

//------------------------------------------------
// setObjVarScript
//------------------------------------------------

void setObjVarScript( obj_id self, string sSentObjVar )
{
	if ( sSentObjVar == "static" )
	{
		// Always make scanners wander.
		if ( hasObjVar( self, "spawn_objvar2" ) )
		{
			if ( "customs" == getStringObjVar( self, "spawn_objvar2" ) )
			{
				string script = "city.city_wander";
				attachScript( self, script );				
				return;
			}
		}
		
		if ( !hasScript(self, "ai.ai") )
			attachScript( self, "ai.ai" );
		stop( self );
		ai_lib.setDefaultCalmBehavior( self, ai_lib.BEHAVIOR_SENTINEL );
	}
	else if ( sSentObjVar == "default" )
	{
		if ( !hasScript(self, "ai.ai") )
			attachScript( self, "ai.ai" );
		ai_lib.setDefaultCalmBehavior( self, ai_lib.BEHAVIOR_WANDER );
	}
	else if ( sSentObjVar == "city_wander" )
	{
		string script = "city.city_wander";
		attachScript( self, script );
	}
	else if ( sSentObjVar == "covert_droid" )
	{
		// This must have never been implemented.
//		string script = "NEED_SCRIPT";
//		attachScript( self, script );
	}
	else if ( sSentObjVar == "customs" )
	{
		string script = "ai.imperial_presence.harass";
		attachScript( self, script );
	}
	else if ( sSentObjVar == "scan_follow" )
	{
		string script = "city.imperial_crackdown.st_static";
		attachScript( self, script );
	}

	return;
}

//------------------------------------------------
// OnIncapacitated
//------------------------------------------------

trigger OnIncapacitated( obj_id killer )
{
	obj_id mom = getObjIdObjVar( self, "mom" );
	if ( mom == null )
		return SCRIPT_CONTINUE;

	int spawnNum = getIntObjVar( self, "spawn_number" );
	if ( spawnNum < 0 )
		return SCRIPT_CONTINUE;

	int respawnTime = getIntObjVar( self, "respawn_time" );
	if ( respawnTime == 0 )
		respawnTime = 300;

	dictionary info = new dictionary();
	info.put( "spawnNumber", spawnNum );
	info.put( "spawnMob", self );
	messageTo( mom, "tellingMomIDied", info, respawnTime, false );

	return SCRIPT_CONTINUE;
}

include library.chat;

inherits theme_park.newbie_tutorial.tutorial_base;

messageHandler handleInitiateDialog()
{
	faceToBehavior( self, getPlayer( self ));
	chat.chat( self, new string_id ( NEWBIE_CONVO, "quarter_greeting" ));
	doAnimationAction( self, "greet" );
	return SCRIPT_CONTINUE;
}

trigger OnObjectMenuRequest(obj_id player, menu_info mi)
{
	obj_id bldg = getTopMostContainer( self );
	obj_id term = getObjIdObjVar( bldg, TRAVEL_TERMINAL );
	makeStaticWaypoint( term );

	int mnu = mi.addRootMenu (menu_info_types.CONVERSE_START, null);
	menu_info_data mdata = mi.getMenuItemById(mnu);
	mdata.setServerNotify(false);
	return SCRIPT_CONTINUE;
}

trigger OnStartNpcConversation (obj_id speaker)
{
	string quarter_1_go = "quarter_1_go";
	string limitStartingLocations = getConfigSetting("New_Player", "LimitStartingLocations");
	if ( limitStartingLocations != null && limitStartingLocations.equals("true") )
	{
		quarter_1_go = "quarter_1_go_nochoice";
	}
	
	if ( hasObjVar( speaker, "newbie.canUseTravelTerminal") )
	{
		
		chat.chat( self, new string_id ( NEWBIE_CONVO, quarter_1_go));
		doAnimationAction( self, "point_right" );
		return SCRIPT_CONTINUE;
	}

	if ( !hasObjVar( speaker, "newbie.hasReleaseDocuments" ))
	{
		chat.chat( self, new string_id ( NEWBIE_CONVO, "quarter_nodocs" ));
		doAnimationAction( self, "smack_self" );
		return SCRIPT_CONTINUE;
	}


	faceToBehavior( self, speaker );
	string_id greeting = new string_id (NEWBIE_CONVO, "quarter_1_start");//Taking your release docs.
	string_id response[] = new string_id [2];
	response[0] = new string_id (NEWBIE_CONVO, "quarter_1_reply_1");//Can I leave now?
	response[1] = new string_id (NEWBIE_CONVO, "quarter_1_reply_2");//Where are you sending me?

	obj_id wayp = getObjIdObjVar( speaker, "newbie.hasReleaseDocuments");
	setObjVar( speaker, "newbie.canUseTravelTerminal", true );
	destroyWaypointInDatapad(wayp, speaker);
	removeObjVar( speaker, "newbie.hasReleaseDocuments");

	npcStartConversation (speaker, self, CONVO, greeting, response);

	return SCRIPT_CONTINUE;
}

trigger OnNpcConversationResponse(string convo, obj_id player, string_id response)
{
	string quarter_1_go = "quarter_1_go";
	string limitStartingLocations = getConfigSetting("New_Player", "LimitStartingLocations");
	if ( limitStartingLocations != null && limitStartingLocations.equals("true") )
	{
		quarter_1_go = "quarter_1_go_nochoice";
	}
	
	if ( response.getAsciiId().equals("quarter_1_reply_1") ||
		 response.getAsciiId().equals("quarter_1_reply_2")
		)//Where is the quartermaster? or Will Do.
		{
			string_id message = new string_id (NEWBIE_CONVO, quarter_1_go);//Use the travel terminal
			npcSpeak( player, message );
			doAnimationAction( self, "point_right" );
			npcEndConversation( player );
		}

	return SCRIPT_CONTINUE;
}


include library.chat;
include library.ai_lib;

inherits theme_park.newbie_tutorial.tutorial_base;

trigger OnAttach()
{
	queueCommand( self, ##"setMood", self, "unhappy", COMMAND_PRIORITY_DEFAULT);
	return SCRIPT_CONTINUE;
}

messageHandler initiatePanic()
{
	obj_id trooper = getObjIdObjVar( getTopMostContainer( self ), TROOPER1 );
	if ( !isIdValid( trooper ) )
	{
	 	trooper = getObjIdObjVar( getTopMostContainer( self ), TROOPER2 );
		if ( !isIdValid( trooper ))
	 		return SCRIPT_CONTINUE;
	}
	setMovementRun( self );
	location destLoc = new location( getLocation( self ) );

	destLoc.x = MOUSE_DROID_DEST_X;
	destLoc.y = MOUSE_DROID_DEST_Y;
	destLoc.z = MOUSE_DROID_DEST_Z;
	destLoc.cell = getCellId( getTopMostContainer( self ), "r4" );

	setHomeLocation( self, destLoc );
	pathTo( self, destLoc);
	chat.setChatMood( self, chat.MOOD_SCARED);
	chat.chat( self, chat.CHAT_SHOUT, new string_id(NEWBIE_CONVO,"shout_panic1"));
	setObjVar( self, "newbie.shoutNum", 1 );
	messageTo( self, "handleNextYell", null, 10, false );
	return SCRIPT_CONTINUE;
}

messageHandler handleNextYell()
{
	setMovementRun( self );
	int shoutNum = getIntObjVar( self, "newbie.shoutNum" );
	shoutNum++;
	chat.chat( self, chat.CHAT_SHOUT, new string_id(NEWBIE_CONVO,"shout_panic"+shoutNum));
	setObjVar( self, "newbie.shoutNum", shoutNum );
	if ( shoutNum < 3 )
		messageTo( self, "handleNextYell", null, 10, false );

	return SCRIPT_CONTINUE;
}

trigger OnMovePathComplete()
{
	ai_lib.setMood( self, "npc_sad");
	setMovementWalk( self );
	messageTo( self, "handleWarnTroopers", null, 0, false );
	return SCRIPT_CONTINUE;
}

trigger OnBehaviorChange( int newBehavior, int oldBehavior, int[] changeFlags )
{
	return SCRIPT_OVERRIDE;
}

messageHandler handleWarnTroopers()
{
	obj_id player = getPlayer( self );
	if ( player == null )
		return SCRIPT_CONTINUE;

	obj_id currentRoom = getContainedBy( player );
	obj_id bldg = getTopMostContainer( player );
	obj_id roomId = getCellId( bldg, "r4" );

	if ( currentRoom == roomId )
	{
		faceToBehavior( self, player );
		doAnimationAction( self, "gesticulate_wildly" );
		chat.chat( self, chat.CHAT_SHOUT, new string_id(NEWBIE_CONVO,"shout_panic4"));
		messageTo( self, "handleWarnTroopers", null, 20, false );
	}
	return SCRIPT_CONTINUE;
}


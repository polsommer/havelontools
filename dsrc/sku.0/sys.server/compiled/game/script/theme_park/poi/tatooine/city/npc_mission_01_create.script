
include library.ai_lib;
include library.create;
const string npcTable = "datatables/poi/city/convo_npc.iff";

trigger OnAttach ()
{
	spawnGuyOne( self );
	spawnGuyTwo( self );
	messageTo( self, "handleChatting", null, 10, false );
	messageTo( self, "checkForScripts", null, 5, false );
	return SCRIPT_CONTINUE;
}
trigger OnInitialize()
{
	if(hasObjVar(self, "alreadySpawned"))
	{
		removeObjVar(self, "alreadySpawned");
	}
	
	spawnGuyOne( self );
	spawnGuyTwo( self );
	messageTo( self, "handleChatting", null, 10, false );
	messageTo( self, "checkForScripts", null, 5, false );
	
	return SCRIPT_CONTINUE;


}

messageHandler handleChatting()
{
	messageTo( self, "handleChatting", null, 600, false );
	obj_id guy1 = getObjIdObjVar( self, "guy1" );
	obj_id guy2 = getObjIdObjVar( self, "guy2" );

	if ( !exists(guy1) || !exists(guy2) )
		return SCRIPT_CONTINUE;

	faceTo (guy1, guy2);
	faceTo (guy2, guy1);
	setAnimationMood (guy1, "conversation");
	setAnimationMood (guy2, "conversation");
	return SCRIPT_CONTINUE;
}

messageHandler handleGuyOneKilled()
{
	spawnGuyOne( self );
	return SCRIPT_CONTINUE;
}

messageHandler handleGuyTwoKilled()
{
	spawnGuyTwo( self );
	return SCRIPT_CONTINUE;
}

void spawnGuyOne( obj_id baseObject )
{
	obj_id guy1 = create.themeParkObject( getRandomGuy(), 1,0, "handleGuyOneKilled", 0 );
	attachScript (guy1, "theme_park.poi.tatooine.city.npc_mission_01_convo");
	setInvulnerable(guy1, true);
	setObjVar( baseObject, "guy1", guy1 );
}

void spawnGuyTwo( obj_id baseObject )
{
	obj_id guy2 = create.themeParkObject( getRandomGuy(), 1,1, "handleGuyTwoKilled", 0 );
	attachScript (guy2, "theme_park.poi.tatooine.city.npc_mission_01_friend_convo");
	setInvulnerable(guy2, true);
	setObjVar( baseObject, "guy2", guy2 );
}

string getRandomGuy ()
{
	location here = getLocation (getSelf());
	string planet = here.area;
	string[] npcList = dataTableGetStringColumnNoDefaults( npcTable, planet);
	int npcNum = rand(0, npcList.length -1 );
	string npc = npcList[ npcNum];
	return npc;
}

messageHandler checkForScripts()
{
	if (hasScript (self, "theme_park.poi.launch"))
	{
		detachScript (self, "theme_park.poi.launch");
	}		
	return SCRIPT_CONTINUE;
}

include library.callable;
include library.create;
include library.utils;

//constants
const string STARTER_SLOT			= "meatlump_wine_starter_slot";
const string COLLECTION_NAME 			= "col_meatlump_wine_01";
const string SLOT_NAME				= "meatlump_wine_juicer_slot";
const float UPDATE_RADIUS 			= 0.5f;
const string_id SID_COLLECT_JUICE		= new string_id("collection", "meatlump_collect_juice");

trigger OnAttach()
{
	createTriggerVolume("meatlump_rodent_trigger_volume", UPDATE_RADIUS, true); 
	return SCRIPT_CONTINUE;
}

trigger OnTriggerVolumeEntered(string volName, obj_id breecher)
{
	//only players can breech unless you write code specfically for AI.
	if(!isIdValid(breecher) || !exists(breecher))
	{
		return SCRIPT_CONTINUE;
	}
	
	//get current location
	location startLocation = getLocation(self);
	location newLocation = null;

	//this will stop the system messages if they have finished the slot, but not turned in the collection
	if(hasCompletedCollectionSlot(breecher, "meatlump_wine_juicer_slot") && !hasCompletedCollection(breecher, COLLECTION_NAME))
	{
		return SCRIPT_CONTINUE;
	}
	
	if(!hasCompletedCollectionSlotPrereq(breecher, SLOT_NAME) || hasCompletedCollection(breecher, COLLECTION_NAME))
	{
		//loop 10 times looking for a good location in a circle 
		for(int i = 0; i < 10; ++i)
		{
			newLocation = utils.getRandomLocationInRing(startLocation, 10.0f, 10.0f);
			
			//if we are in an interior we want to verify we dont go out of bounds
			if(isIdValid(newLocation.cell))
			{
				if(isValidInteriorLocation(newLocation))
				{
					string creatureName = getCreatureName(self);
					if(creatureName.equals("meatlump_worrt"))
					{
						setMovementPercent(self, 3.0f);
					}
					else if(creatureName.equals("meatlump_kreetle"))
					{
						setMovementPercent(self, 1.0f);
					}
					pathTo(self, newLocation);
					break;
				}
			}
		}
	}
	else
	{
		//kill the creature - loot - modify collection/quest increment
		//Make vulnerable so we can kill it.
		setInvulnerable(self, false);
		//kill it
		setHealth(self, -1000);
		//remove trigger volume to prevent further updates
		removeTriggerVolume("meatlump_rodent_trigger_volume");
		//modify slot value
		modifyCollectionSlotValue(breecher, "meatlump_wine_juicer_slot", 1);
		//tell the player
		sendSystemMessage(breecher, SID_COLLECT_JUICE);
	}
	return SCRIPT_CONTINUE;
}


//------------------------------------------------
// recovery_target.script
// Brandon Reinhart
//
// This script resides on the ship being recovered.
//------------------------------------------------

//------------------------------------------------
// Includes
//------------------------------------------------

include library.space_quest;
include library.space_utils;
include library.utils;
include library.prose;

//------------------------------------------------
// OnAttach
//------------------------------------------------

trigger OnAttach()
{
	// We don't want to hang around too long.
	// Warp us out if we are still alive after X minutes.
	messageTo( self, "pendingWarp", null, 3600.f, false );

 	return SCRIPT_CONTINUE;
}

//------------------------------------------------
// warpOut
//------------------------------------------------

messageHandler pendingWarp()
{
	// Warp us out if we are around for too long.
	obj_id quest = getObjIdObjVar( self, "quest" );
	if ( isIdValid( quest ) && exists( quest ) )
		setObjVar( quest, "critical_warped_recovery", 1 );
	destroyObjectHyperspace( self );

	return SCRIPT_CONTINUE;
}

//------------------------------------------------
// OnShipDisabled
//------------------------------------------------

messageHandler OnShipDisabled()
{
	obj_id quest = getObjIdObjVar( self, "quest" );
	space_utils.notifyObject( quest, "recoverShipDisabled", null );

	return SCRIPT_CONTINUE;
}

//------------------------------------------------
// selfDestruct
//------------------------------------------------

messageHandler selfDestruct()
{
	// We do not self destruct.
	return SCRIPT_OVERRIDE;
}

//------------------------------------------------
// objectDestroyed
// Called from the combat system when the ship is destroyed.
//------------------------------------------------

messageHandler objectDestroyed()
{
	obj_id quest = getObjIdObjVar( self, "quest" );
	dictionary outparams = new dictionary();
	outparams.put( "ship", self );
	outparams.put( "reason", 1 );
	space_utils.notifyObject( quest, "recoveryFailed", outparams );

 	return SCRIPT_CONTINUE;
}

//------------------------------------------------
// missionAbort
//------------------------------------------------

messageHandler missionAbort()
{
	destroyObjectHyperspace( self );

	return SCRIPT_CONTINUE;
}

//------------------------------------------------
// registerDestination
//------------------------------------------------

messageHandler registerDestination()
{
	if ( params == null )
		return SCRIPT_CONTINUE;

	// Set our final destination notification.
	obj_id quest = params.getObjId( "quest" );
	location loc = params.getLocation( "loc" );
	string dest = params.getString( "dest" );
	addLocationTarget3d( dest, loc, 300 );
	obj_id player = params.getObjId( "player" );
	setObjVar( self, "player", player );

	// Store reference to our quest parent.
	setObjVar( self, "quest", quest );

	if ( dest.equals( "recovery" ) )
	{
		removeLocationTarget( "escape" );
	}

	return SCRIPT_CONTINUE;
}

//------------------------------------------------
// OnArrivedAtLocation
//
// We may have arrived at a key quest location.
// Forward this message to our quest objects.
//------------------------------------------------

trigger OnArrivedAtLocation( string name )
{
	// Forward the notification to our quest object.
	obj_id quest = getObjIdObjVar( self, "quest" );
	if ( name.equals( "recovery" ) )
	{
		// We arrived at our warp out spot.
		dictionary params = new dictionary();
		params.put( "ship", self );
		space_utils.notifyObject( quest, "recoveryComplete", params );
	}
	else if ( name.equals( "escape" ) )
	{
		// We were not recovered in time.
		dictionary params = new dictionary();
		params.put( "ship", self );
		params.put( "reason", 0 );
		space_utils.notifyObject( quest, "recoveryFailed", params );
	}

	return SCRIPT_CONTINUE;
}

//------------------------------------------------
// OnDestroy
//------------------------------------------------

trigger OnDestroy()
{
	obj_id quest = getObjIdObjVar( self, "quest" );
	if ( isIdValid( quest ) && exists( quest ) && hasObjVar( quest, "critical_warped_recovery" ) )
	{
		dictionary outparams = new dictionary();
		outparams.put( "ship", self );
		messageTo( quest, "warpoutFailureRecovery", null, 2.f, false );
	}
	return SCRIPT_CONTINUE;
}
include library.space_transition;
include library.space_utils;
include library.utils;

// ======================================================================
//
// turret_falcon.script
//
// Copyright 2004 Sony Online Entertainment
//
// ======================================================================

const string_id SID_PILOT        = new string_id("space/space_interaction", "pilot_ship");
const string_id SID_TURRET_UPPER = new string_id("space/space_interaction", "turret_upper");
const string_id SID_TURRET_LOWER = new string_id("space/space_interaction", "turret_lower");

// ======================================================================

trigger OnObjectMenuRequest(obj_id player, menu_info mi)
{
	obj_id objShip = space_transition.getContainingShip(player);

	//just the upper turret
	if (utils.hasScriptVar(player, "npe.readyforturret"))
	{
		mi.addRootMenu(menu_info_types.ITEM_USE, SID_TURRET_UPPER);

	}

	sendDirtyObjectMenuNotification(self);

	return SCRIPT_CONTINUE;
}

// ----------------------------------------------------------------------

trigger OnObjectMenuSelect(obj_id player, int item)
{
	// check if the weapon si equipped
	// gunner0 is weapon _

	if (item == menu_info_types.ITEM_USE)
	{
		obj_id objShip = space_transition.getContainingShip(player);

		if (!utils.hasScriptVar(player, "npe.readyforturret"))
		{
			string_id message = new string_id("npe_millennium_falcon", "ladder_disabled");
			sendSystemMessage(player, message);

			return SCRIPT_CONTINUE;
		}

		if(hasTurretWeapon(objShip, ship_chassis_slot_type.SCST_weapon_0, player))
		{
			if (!equip(player, self, "ship_gunner0_pob"))
			{
				string_id strSpam = new string_id("space/space_interaction", "turret_occupied");
				sendSystemMessage(player, strSpam);
			}
			else
			{
				if (!utils.hasScriptVar(player, "firstTurret"))
				{
					obj_id building = getTopMostContainer(self);
					messageTo(building, "continueMainTable", null, 1, false);
					messageTo(building, "spawnShips", null, 0 , false);
					messageTo(building, "phaseTwo", null, 2, false);
					utils.setScriptVar(player, "firstTurret", "yes");
					utils.setScriptVar(player, "npe.finishedTurret", false);
				}
			}
		}

	}
	return SCRIPT_CONTINUE;
}

// ======================================================================

boolean hasTurretWeapon(obj_id objShip, int intSlot, obj_id objPlayer)
{
	if(!isShipSlotInstalled(objShip, intSlot))
	{
		string_id strSpam = new string_id("space/space_interaction", "no_turret");
		sendSystemMessage(objPlayer, strSpam);
		return false;
	}
	if(isShipComponentDisabled(objShip, intSlot))
	{

		string_id strSpam = new string_id("space/space_interaction", "turret_disabled");
		sendSystemMessage(objPlayer, strSpam);
		return false;
	}
	return true;
}
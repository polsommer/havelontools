// Bestine politician event master script. 
// Tracks the state of the politician event
// and monitors when the election begins and ends.

include library.utils;


// const int ELECTION_DURATION = 2592000; // election lasts 30 days
// const int UNTIL_NEXT_ELECTION = 2592000; // office is held for 60 days, but the next election starts halfway through that.

trigger OnAttach()
{
	//begin election
	messageTo(self, "beginBestineElection", null, 1, false);
	return SCRIPT_CONTINUE;
}

int getElectionDuration()
{
	string durationConfigSetting = getConfigSetting("BestineEvents",  "PoliticianEventDuration");
	int electionDuration = utils.stringToInt (durationConfigSetting);
	if (electionDuration == 0 || electionDuration == -1)
		electionDuration = 2592000; // 30 days
		
	return electionDuration;
}

messageHandler beginBestineElection()
{
	int electionNum = 1;
	if ( hasObjVar(self, "bestine.electionEnded") )
	{
		electionNum = getIntObjVar(self, "bestine.electionEnded") + 1;
		removeObjVar(self, "bestine.electionEnded");
	}
	
	if ( !hasObjVar(self, "bestine.electionWinner") )
	{
			setObjVar(self, "bestine.electionWinner", "none");
	}
	
	if ( hasObjVar(self, "bestine.timeNextElectionStarts") )
		removeObjVar(self, "bestine.timeNextElectionStarts");
	
	setObjVar(self, "bestine.electionStarted", electionNum);
	setObjVar(self, "bestine.votesForSean", 0);
	setObjVar(self, "bestine.votesForVictor", 0);
	
	
	obj_id tatooineCapitolObjId = getObjIdObjVar(self, "bestine.tatooineCapitolObjId");
	messageTo(tatooineCapitolObjId, "handlePoliticianEventStateChange", null, 1, false);
	
	// End the election after it's duration has run.
	int electionDuration = getElectionDuration();
	messageTo(self, "endBestineElection", null, electionDuration, true);
	return SCRIPT_CONTINUE;
}

messageHandler endBestineElection()
{
	int electionNum = 1;
	if ( hasObjVar(self, "bestine.electionStarted") )
	{
		electionNum = getIntObjVar(self, "bestine.electionStarted");
		removeObjVar(self, "bestine.electionStarted");
	}

	if ( !hasObjVar(self, "bestine.electionWinner") )
	{
		setObjVar(self, "bestine.electionWinner", "none");
	}
		
	setObjVar(self, "bestine.electionEnded", electionNum);
	removeObjVar(self, "bestine.votesForSean");
	removeObjVar(self, "bestine.votesForVictor");
	
	obj_id tatooineCapitolObjId = getObjIdObjVar(self, "bestine.tatooineCapitolObjId");
	messageTo(tatooineCapitolObjId, "handlePoliticianEventStateChange", null, 1, false);
	
	// Begin the next election after the required duration.
	int electionDuration = getElectionDuration();
	int timeNextElectionStarts = getGameTime() + electionDuration;
	setObjVar(self, "bestine.timeNextElectionStarts", timeNextElectionStarts);
	messageTo(self, "beginBestineElection", null, electionDuration, true);
	return SCRIPT_CONTINUE;
}

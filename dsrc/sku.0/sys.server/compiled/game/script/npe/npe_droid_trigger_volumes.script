include library.chat;
include library.create;
include library.utils;
include library.groundquests;

trigger OnCreatureDamaged(obj_id player, obj_id weapon, int[] damage)
{
	if (utils.hasScriptVar (self, "droidVolumeBreach"))
	{
		return SCRIPT_CONTINUE;
	}

	if (!isPlayer (player))
	{
		return SCRIPT_CONTINUE;
	}

	string myname = getName (self);
	string guard = "npe_rakqua_warrior";
	dictionary webster = new dictionary ();

	webster.put ("player", player);

	if (myname != null && myname != "")
	{
		utils.setScriptVar (self, "droidVolumeBreach", 1);
		if (myname == "mob/creature_names:npe_droid_rescind")
		{
			playExplosionEffect(player, self, -400);
		}
		else if (myname == "mob/creature_names:npe_k3i_buzzer")
		{
			playExplosionEffect(player, self, -250);
		}
		else if (myname == "mob/creature_names:npe_4warning" )
		{
			webster.put ("guard", guard);
			messageTo (self, "spawnGuards", webster, 5, false);
		}
		else if (myname == "mob/creature_names:npe_i10_probe")
		{
			guard = "npe_rakqua_shaman";
			webster.put ("guard", guard);
			messageTo (self, "spawnGuards", webster, 5, false);
		}
		else if (myname == "npc_name:typho")
		{
			playExplosionEffect(player, self, -150);
		}
		else if (myname == "mob/creature_names:npe_smuggler_trap")
		{
			//debugSpeakMsg (player, "I'm me");
			if (groundquests.isTaskActive(player, "npe_smuggler_try", "droidAlert"))
			{
				debugSpeakMsg (player, "You have the quest");
				groundquests.sendSignal(player, "gotYou");
				guard = "npe_rakqua_warrior";
				webster.put ("guard", guard);
				messageTo (self, "spawnGuards", webster, 5, false);
			}
		}
		else
		{
			return SCRIPT_CONTINUE;
		}
	}

	return SCRIPT_CONTINUE;
}

void playExplosionEffect(obj_id player, obj_id self, int damageAmount)
{
	playClientEffectLoc(player, "clienteffect/combat_grenade_small_01.cef", getLocation(self), 1f);
	addAttribModifier(player, HEALTH, damageAmount, 0f, 0f, MOD_POOL);
	kill(self);
	messageTo (self, "getRidOfMe", null, 1, true);
	return;
}

messageHandler spawnGuards()
{
	string guard = params.getString ("guard");
	obj_id player = params.getObjId ("player");

	playClientEffectObj(self, "clienteffect/survey_tool_gas.cef", self, "");

	location me = getLocation (self);
	me.x = me.x + 1;
	if (guard != null && guard != "")
	{
		create.object (guard, me);
		me.y = me.y + 1;
		create.object (guard, me);
	}
	chat.chat (self, "INTRUDER ALERT! INTRUDER ALERT!");
	detachScript (self, "npe.npe_droid_trigger_volumes");
	return SCRIPT_CONTINUE;
}

messageHandler getRidOfMe()
{
	destroyObject (self);
	return SCRIPT_CONTINUE;
}
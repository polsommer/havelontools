/********************************************************************************************************************************************
 * Copyright (c)2000-2002 Sony Online Entertainment Inc.
 * All Rights Reserved
 *
 * Title:        fs_medic_heal_pack.script
 * Description:  Force Sensitive Quest Item script to display cure values.
 * @author       Benjamin Norris
 * @version      $Revision:$
 **********************************************************************/


/***** INCLUDES ********************************************************/
include library.prose;
include library.utils;

/***** CONSTANTS *******************************************************/
const string CRAFT_VAR						= "crafting_components.";
const string CURE_SYMPTOM_ONE				= "cureSymptomOne";
const string CURE_SYMPTOM_TWO				= "cureSymptomTwo";
const string GIVE_SYMPTOM_ONE				= "giveSymptomOne";

/***** TRIGGERS ********************************************************/


//Displays Heal values.
trigger OnGetAttributes(obj_id player, string[] names, string[] attribs)
{
	int idx = utils.getValidAttributeIndex(names);
	if (idx == -1)
		return SCRIPT_CONTINUE;

   	if (hasObjVar(self, CRAFT_VAR + CURE_SYMPTOM_ONE))
   	{
   		names[idx] = "fs_medic_pack_cure_one";
   		attribs[idx] = _getCureName(self, CRAFT_VAR + CURE_SYMPTOM_ONE);
   		idx++;
   		if (idx >= names.length)
   			return SCRIPT_CONTINUE;
   	}

   	if (hasObjVar(self, CRAFT_VAR + CURE_SYMPTOM_TWO))
   	{
		int cureOne = (int)getFloatObjVar(self, CRAFT_VAR + CURE_SYMPTOM_ONE);
		int cureTwo = (int)getFloatObjVar(self, CRAFT_VAR + CURE_SYMPTOM_TWO);
  		if (cureOne!= cureTwo)
  		{
			names[idx] = "fs_medic_pack_cure_two";
			attribs[idx] = _getCureName(self, CRAFT_VAR + CURE_SYMPTOM_TWO);
			idx++;
			if (idx >= names.length)
				return SCRIPT_CONTINUE;
		}
   	}

   	if (hasObjVar(self, CRAFT_VAR + GIVE_SYMPTOM_ONE))
   	{
   		int cureOne = (int)getFloatObjVar(self, CRAFT_VAR + CURE_SYMPTOM_ONE);
   		int cureTwo = (int)getFloatObjVar(self, CRAFT_VAR + CURE_SYMPTOM_TWO);
  		int symptomOne = (int)getFloatObjVar(self, CRAFT_VAR + GIVE_SYMPTOM_ONE);
  		if (symptomOne!= cureOne && symptomOne != cureTwo)
  		{
			names[idx] = "fs_medic_pack_symptom_one";
			attribs[idx] = _getCureName(self, CRAFT_VAR + GIVE_SYMPTOM_ONE);
			idx++;
			if (idx >= names.length)
				return SCRIPT_CONTINUE;
		}
   	}


	return SCRIPT_CONTINUE;
}



/**** PRIVATE FUNCTIONS ************************************************

/***********************************************************************
 * @brief	translates the numerical value on the medic item into text
 * @brief	to display to the player.
 *
 * @param	self
 *
 * @return	return string
 *********************************************************************/
string _getCureName(obj_id item, string cureVarLoc)
{
	int cureNumber = (int)utils.getFloatObjVar(item, cureVarLoc);
	string_id cure = new string_id ("quest/force_sensitive/fs_medic", "symptom"+cureNumber);
	return localize(cure);
}
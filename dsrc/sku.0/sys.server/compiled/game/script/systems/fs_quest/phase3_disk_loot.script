include library.utils;

const string FREQUENCY_DISK = "object/tangible/loot/quest/force_sensitive/camp_frequency_datapad.iff";
const string WAYPOINT_DISK = "object/tangible/loot/quest/force_sensitive/camp_waypoint_datapad.iff";
const int PERCENT_CHANCE_OF_LOOT = 20; // 20%
const int LIFE_SPAN			= 1000;

trigger OnAttach()
{
	messageTo(self, "msgSelfDestruct", null, (float)LIFE_SPAN, false);
	return SCRIPT_CONTINUE;
}

messageHandler msgSelfDestruct()
{
	if(hasObjVar(self, "dontDestroy"))
	{
		return SCRIPT_CONTINUE;
	}
	
	destroyObject(self);
	return SCRIPT_CONTINUE;
}

// dumps a phase 3 combat quest datadisk loot in the corpse
messageHandler aiCorpsePrepared()
{
	setObjVar(self, "dontDestroy", 1);
	createLoot(self);
	return SCRIPT_CONTINUE;
}

void createLoot(obj_id self)
{
	if(rand(1, 100) < 100-PERCENT_CHANCE_OF_LOOT)
	{
		return;
	}
	
	obj_id corpseInventory = utils.getInventoryContainer(self);
	if(corpseInventory == null)
	{
		return;
	}
	
	string[] loots = new string[]
	{
		FREQUENCY_DISK,
		FREQUENCY_DISK,
		WAYPOINT_DISK
	};
	
	obj_id item = createObject(loots[rand(0, loots.length-1)], corpseInventory, "" );
		
	return;
}

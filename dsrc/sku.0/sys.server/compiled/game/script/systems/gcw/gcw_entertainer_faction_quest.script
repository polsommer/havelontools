include library.utils;
include library.factions;
include library.ai_lib;

trigger OnAttach()
{
	if(!isValidId(self) || !exists(self))
	{
		return SCRIPT_CONTINUE;
	}	
	setObjVar(self, "faction_recruit_quest", 0);
	return SCRIPT_CONTINUE;
}


//The npc is ready to talk to the player.
messageHandler readyToTalk()
{

	//Verify that self is an NPC
	if(!isValidId(self) || !exists(self))
	{
		return SCRIPT_CONTINUE;
	}
	
	obj_id entertainer = utils.getObjIdScriptVar(self, "ai.listeningTo");
	obj_id faction_entertainer = utils.getObjIdScriptVar(self, "faction_ent");
	
	if(!isValidId(entertainer) || (entertainer != faction_entertainer))
	{
		return SCRIPT_CONTINUE;
	
	}
	
	utils.setObjVar(self, "faction_recruit_quest", 1);
	utils.setScriptVar(self, "readyToTalk", faction_entertainer);
	
	//Setting up the particle floating over the NPC's head
	string particle = "";
	if(factions.isImperialorImperialHelper(faction_entertainer))
	{
		particle = "appearance/pt_imperial_symbol.prt";
	}
	
	else if(factions.isRebelorRebelHelper(faction_entertainer))
	{
		particle = "appearance/pt_rebel_symbol.prt";
	}
	
	else
	{
		cleanup(self);
		return SCRIPT_CONTINUE;
	}
	
	obj_id[] particleList = new obj_id[1];
	particleList[0] = faction_entertainer;
	playClientEffectObj(particleList, particle, self, null);
	return SCRIPT_CONTINUE;
}

// The player waited too long. The NPC will no longer engage in conversation with the player
messageHandler lostInterest()
{
	if(!isValidId(self) || !exists(self) || params == null)
	{
		return SCRIPT_CONTINUE;

	}
	
	int paramsTime = params.getInt("lostInterestTime");
	int scriptVarTime = utils.getIntScriptVar(self, "lostInterestTime");
	
	if(paramsTime == scriptVarTime)
	{
		cleanup(self);
		
	}
	return SCRIPT_CONTINUE;
}

messageHandler convinced()
{
	obj_id parent = utils.getObjIdScriptVar(self, "spawnedBy");
	
	params.put("convincedTrooper", self);
	
	messageTo(parent, "createFightingNpc", params, 1.0f, false);
	LOG("mikkel","sent message to spawn a fighting npc");
	return SCRIPT_CONTINUE;
}

messageHandler notConvinced()
{
	LOG("mikkel","notConvinced, calling cleanup");
	cleanup(self);
	
	return SCRIPT_CONTINUE;
}

//Player took too long, convinced the NPC, left or did something else. The NPC should be reset
void cleanup(obj_id self)
{
	if(isValidId(self) && exists(self))
	{
	
		utils.removeScriptVar(self, "ai.oldEntertainerSkillMod");
		utils.removeScriptVar(self, "ai.listeningTo");//stop listening to this guy
		utils.setObjVar(self, "faction_recruit_quest", 0);
		utils.removeScriptVar(self, "faction_ent");		
		stop(self);//cancel that faceToBehavior
		messageTo(self, "resumeDefaultCalmBehavior", null, 0, false);//this will setYaw back to the original pos.	

	}
	
}
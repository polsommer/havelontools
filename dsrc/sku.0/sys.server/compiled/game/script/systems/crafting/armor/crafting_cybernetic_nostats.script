/**
 * Copyright (c) ©2000-2002 Sony Online Entertainment Inc.
 * All Rights Reserved
 *
 * Title:        crafting_armor_nostats.script
 * Description:  base crafting script used to make armor components with No Stats on them
 * @author       $Author: Tblair and Steve 'the man!' Jakab$
 * @version      $Revision: 0$
 */

/**
 * Include Libraries
 */
// include anyLibrary;	/** a .scriptlib file */
include library.utils;
include library.craftinglib;

inherits systems.crafting.armor.crafting_new_armor_clothing_nostats;

/**
 * Constants
 * @{
 */
/** The version number of this script. */
const string VERSION = "v0.00.00";

/******************************/
// example skill data - this will probably go in a derived script
/******************************/

// skill names - the player must have one of these skills to use this script
const string[] REQUIRED_SKILLS = {
	"crafting_cybernetic_novice"
};

// skill mod names - these skill mods will affect the assembly part of the crafting session if the player has them
const string[] ASSEMBLY_SKILL_MODS = {
	"cybernetic_assembly"

};

// skill mod names - these skill mods will affect the experimentation part of the crafting session if the player has them
const string[] EXPERIMENT_SKILL_MODS = {
	"cybernetic_experimentation"
};

// skill mod names - these skill mods will affect the customization part of the crafting session if the player has them
const string[] CUSTOMIZATION_SKILL_MODS = {
	"cybernetic_customization"
};

/**********************
 *
 *  These values are used to determine the resource contribution to the value of item attributes
 *
 **********************/
const resource_weight[] OBJ_ASSEMBLY_ATTRIBUTE_RESOURCES = {
	new resource_weight("condition", new resource_weight.weight[] {
		new resource_weight.weight(craftinglib.RESOURCE_QUALITY,      1),
		new resource_weight.weight(craftinglib.RESOURCE_DECAY_RESIST, 1),
		new resource_weight.weight(craftinglib.RESOURCE_SHOCK_RESIST,    1)
		}
	)
};


/**********************
 *
 *  These values are used to determine the resource max value of item attributes
 *
 **********************/

const resource_weight[] OBJ_MAX_ATTRIBUTE_RESOURCES = {
	new resource_weight("condition", new resource_weight.weight[] {
		new resource_weight.weight(craftinglib.RESOURCE_QUALITY,      1),
		new resource_weight.weight(craftinglib.RESOURCE_DECAY_RESIST, 1),
		new resource_weight.weight(craftinglib.RESOURCE_SHOCK_RESIST,    1)
		}
	)
};
/* @} End constants */



string[] getRequiredSkills()
{
	return REQUIRED_SKILLS;
}

string[] getAssemblySkillMods()
{
	return ASSEMBLY_SKILL_MODS;
}

string[] getExperimentSkillMods()
{
	return EXPERIMENT_SKILL_MODS;
}

string[] getCustomizationSkillMods()
{
	return CUSTOMIZATION_SKILL_MODS;
}

resource_weight[] getResourceMaxResourceWeights()
{
	return OBJ_MAX_ATTRIBUTE_RESOURCES;
}

//--------------------------------------------------------------------------------
//Set the custom colors for the armor.


draft_schematic.custom[] getCustomizations(obj_id player, draft_schematic.custom[] customizations)
{
	if (customizations == null || customizations.length == 0)
	{
		return null;
	}

	//figure out player's customization skillmods
	string[] skills = getCustomizationSkillMods();

	float playerCustomizationMod = 0;
	// grab skillMod values for selected skillMods
	if ( skills != null )
	{
		int[] mods = getEnhancedSkillStatisticModifiers(player, skills);
		for ( int i = 0; i < mods.length; ++i )
		{
			playerCustomizationMod += mods[i];
		}
	}

	// divide current range by 255 to determine how much is in a percentage of the palette.
	//fail-safe on player customization skill-mod value
	if ( playerCustomizationMod > 255 )
	{
		playerCustomizationMod = 255;
	}

	//- the UI only supports 4 colors
	int max_colors = 4;
	int threshold  = 20;

	//set the max number of colors accessible within each color index to be appropriate to player's skill
	for ( int i = 0; i < customizations.length; ++i )
	{
		//-- too many colors
		if (i >= max_colors)
			customizations [i] = null;
		//-- check player skill
		else if (playerCustomizationMod < threshold)
			customizations [i] = null;
		//-- dummy color, skip it
		else if (customizations [i].name.indexOf ("index_color_0") >= 0 && customizations [i].maxValue == 1)
			customizations [i] = null;
		//-- it is customizable
		else
		{
			int max_maxValue = customizations[i].maxValue;

			//-- ensure that round off errors don't prevent the last element from being customized by adding 1

			customizations[i].maxValue = (int)(playerCustomizationMod*((customizations[i].maxValue + 1)/255.0f));

			if (customizations [i].maxValue > max_maxValue)
				customizations [i].maxValue = max_maxValue;
			if (customizations [i].maxValue < customizations [i].minValue)
				customizations [i].maxValue = customizations [i].minValue;

			threshold += 20;
		}
	}

	return customizations;
}

//-----------------------------------------------------------
resource_weight[] getAssemblyResourceWeights()
{
	return OBJ_ASSEMBLY_ATTRIBUTE_RESOURCES;
}



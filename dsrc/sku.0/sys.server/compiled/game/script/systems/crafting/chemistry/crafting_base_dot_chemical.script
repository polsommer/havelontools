include library.craftinglib;
include library.consumable;
include library.player_stomach;

inherits systems.crafting.crafting_base;

/** The version number of this script. */
const string VERSION = "v1.00.00";


/**
 * Merge the crafting dictionary into the item attributes, and write them to the prototype.
 *
 * @param prototype						the prototype object
 * @param itemAttributes				attributes of the prototype
 * @param craftingValuesDictionary      dictionary containing component updates to the item attributes
 **/
void calcAndSetPrototypeProperties ( obj_id prototype, draft_schematic.attribute[] itemAttributes, dictionary craftingValuesDictionary )
{
	super.calcAndSetPrototypeProperties ( prototype, itemAttributes, craftingValuesDictionary );
}

/* -----------------3/4/2002 1:48PM------------------
 * Function calcAndSetPrototypeProperties
 *
 * This function reads in the final item attributes that are about to be written into the manufacturing schematic, looks up modifications to these attributes
 * due to component-ingredients from the craftingValuesDictionary, sums them, and then writes the final property values to the prototype item.
 * NOTE: Whatever happens, make sure to cast the attribute array data into the appropriate form before setting the property. The item will want float instead of int for some properties
 *
 * @param prototype - this is the obj_id of the prototype object
 * @param draft_schematic.slot[] itemAttributes - this is the array of all of the item's inital Attributes. These will be updated with the skill-check and resource assembly results.
 *
 * @return - this function doesn't return anything, rather, it updates the prototype's property values with the results of the assembly phase
 * --------------------------------------------------*/
void calcAndSetPrototypeProperties ( obj_id prototype, draft_schematic.attribute[] itemAttributes)
{
	//initialize storage for power
	int tempPower = 0;
	int[] skill_value = {0};

	// here we go...
	debugServerConsoleMsg(null, "Beginning assembly-phase prototype property setting");
	for ( int i = 0; i < itemAttributes.length; ++i ) // go through all attributes
	{
		if ( itemAttributes[i] == null )
			continue;
		if (!calcAndSetPrototypeProperty ( prototype, itemAttributes[i]))
		{
			//load initial object properties, sum them with experimentation results and component mods, then write them back onto the object
			if ( itemAttributes[i].name.getAsciiId().equals("power"))
			{
				setObjVar (prototype, "healing.dot_power", (int) itemAttributes[i].currentValue);
			}
			else if ( itemAttributes[i].name.getAsciiId().equals("charges"))
			{
				setCount( prototype, (int) itemAttributes[i].currentValue );
			}
			else if ( itemAttributes[i].name.getAsciiId().equals("skillModMin"))
			{
				skill_value[0] = (int) ((itemAttributes[i].maxValue + itemAttributes[i].minValue) - itemAttributes[i].currentValue);
			}
			else if ( itemAttributes[i].name.getAsciiId().equals("range"))
			{
				int range = (int) itemAttributes[i].currentValue;
				if (range > 48)
					range = 48;

				setObjVar (prototype, "healing.range", range);
			}
			else if ( itemAttributes[i].name.getAsciiId().equals("area"))
			{
				setObjVar (prototype, "healing.area", (int) itemAttributes[i].currentValue);
			}
			else if ( itemAttributes[i].name.getAsciiId().equals("potency"))
			{
				setObjVar (prototype, "healing.dot_potency", (int) itemAttributes[i].currentValue);
			}
			else if ( itemAttributes[i].name.getAsciiId().equals("duration"))
			{
				setObjVar (prototype, "healing.dot_duration", (int) itemAttributes[i].currentValue);
			}
			else
			{
				setObjVar (prototype, craftinglib.COMPONENT_ATTRIBUTE_OBJVAR_NAME + "." + itemAttributes[i].name.getAsciiId(),
					itemAttributes[i].currentValue);
			}
		}
	}

	// Add in skill mod required data
	string[] skill_mod = {"combat_healing_ability"};
	setObjVar(prototype, consumable.VAR_SKILL_MOD_REQUIRED, skill_mod);			// Suppress linting - We know that this array will
																				// always be at least one element long
	// Add in min mod value data
	setObjVar(prototype, consumable.VAR_SKILL_MOD_MIN, skill_value);			// Suppress linting - We know that this array will
}																				// always be at least one element long

include library.permissions;
include library.utils;

const string STF_FILE = "event_perk";

trigger OnAttach()
{
	obj_id[] rewarded = new obj_id[1];
	rewarded[0] = self;
	setObjVar(self, "event_perk.chest.rewarded", rewarded);

	return SCRIPT_CONTINUE;
}

// If the owner tries to open us, make them the owner
trigger OnAboutToOpenContainer(obj_id whoIsOpeningMe)
{
	obj_id owner = getObjIdObjVar(self, "event_perk.owner");
	

	if(whoIsOpeningMe == owner)
		setOwner(self, owner);
	
	return SCRIPT_CONTINUE;
}

// When owner is done, remove them to allow others who should have access.
trigger OnClosedContainer(obj_id whoClosedMe)
{
	obj_id owner = getObjIdObjVar(self, "event_perk.owner");
	
	if(whoClosedMe == owner)
	{
		string noOwnerString = "0";
		obj_id noOwner = utils.stringToObjId(noOwnerString);

		setOwner(self, noOwner);
	}
	
	return SCRIPT_CONTINUE;
}

// Only one per customer. Record anyone that removes an item.
trigger OnAboutToLoseItem(obj_id srcContainer, obj_id transferer, obj_id item)
{


	obj_id owner = getObjIdObjVar(self, "event_perk.owner");

	obj_id[] rewarded = getObjIdArrayObjVar(self, "event_perk.chest.rewarded");

	//-- Whatever you're the owner so go nuts.
	if(transferer == owner)
		return SCRIPT_CONTINUE;

	//-- You're not the owner. Have you been rewarded?

	if(transferer != owner)
	{
		for(int i = 0; i < rewarded.length; i++)
		{
			// You've been rewarded. Puke and die.
			if(rewarded[i] == transferer)
			{
				sendSystemMessage(transferer, new string_id(STF_FILE, "chest_only_one_item"));		
				return SCRIPT_OVERRIDE;
			}	
		}

		//-- Otherwise add you to the list.
		
		obj_id newRewarded[] = new obj_id[rewarded.length + 1];
		newRewarded[rewarded.length] = transferer;
		
		for(int i = 0; i < rewarded.length; i++)
		{
			newRewarded[i] = rewarded[i];
		}

		setObjVar(self, "event_perk.chest.rewarded", newRewarded);
		CustomerServiceLog("EventPerk", "(Treasure Chest - [" + self + "] had an item removed from it by player [" + transferer + "].");
	}

	return SCRIPT_CONTINUE;

}

// Only owner can add items.
trigger OnAboutToReceiveItem(obj_id srcContainer, obj_id transferer, obj_id item)
{

	obj_id owner = getObjIdObjVar(self, "event_perk.owner");

	if(transferer != owner)
	{
		sendSystemMessage(transferer, new string_id(STF_FILE, "chest_can_not_add"));
		return SCRIPT_OVERRIDE;
	}

	return SCRIPT_CONTINUE;
}
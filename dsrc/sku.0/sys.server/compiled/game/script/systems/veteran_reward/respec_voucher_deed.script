// Script for an item that makes all future respecs for the character half price
// Vet Reward
// 04/26/06:  PMW

include library.sui;
include library.utils;

const string	STF_FILE = "veteran";
const string_id RESPEC_VOUCHER					= new string_id(STF_FILE, "respec_voucher");
const string_id SID_RESPEC_VOUCHER_TITLE			= new string_id(STF_FILE, "sui_respec_title");
const string_id SID_RESPEC_VOUCHER_PROMPT			= new string_id(STF_FILE, "sui_respec_prompt");

trigger OnObjectMenuRequest(obj_id player, menu_info mi)
{
	mi.addRootMenu(menu_info_types.ITEM_USE, RESPEC_VOUCHER);
	return SCRIPT_CONTINUE;
}

trigger OnObjectMenuSelect(obj_id player, int item)
{
	if ( item == menu_info_types.ITEM_USE )
	{
		string title = utils.packStringId(SID_RESPEC_VOUCHER_TITLE);
		string prompt = utils.packStringId(SID_RESPEC_VOUCHER_PROMPT);
		sui.msgbox(self, player, prompt, sui.YES_NO, title, "handleRespecChoice");
	}

	return SCRIPT_CONTINUE;
}

messageHandler handleRespecChoice()
{
 	obj_id player = sui.getPlayerId(params);
	int btn = sui.getIntButtonPressed(params);

	if ( btn == sui.BP_CANCEL )
	{
		return SCRIPT_CONTINUE;
    	}
    	
    	setObjVar(player, "respec_voucher", 1);
	sendSystemMessage (player, new string_id (STF_FILE, "msg_respec_used"));
	destroyObject(self);
    	   	
  	return SCRIPT_CONTINUE;
}

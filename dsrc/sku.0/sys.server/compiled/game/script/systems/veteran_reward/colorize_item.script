/**********************************************************************
 * Copyright (c)2005 Sony Online Entertainment Inc.
 * All Rights Reserved
 *
 * Title:        systems\veteran_reward\colorize_item.script
 * Description:  calls a UI allowing the player to set colors on an object
 * @author       $Author: Jesse Benjamin$
 * @version      $Revision: 1.0.0 $
 **********************************************************************/
include library.player_structure;
include library.utils;

const string USE_STRING				= "use_string";


trigger OnObjectMenuRequest(obj_id player, menu_info mi)
{
	if(!isIdValid(player))
		return SCRIPT_CONTINUE;


	obj_id topMost = getTopMostContainer(self);
	if(!player_structure.isBuilding(topMost))
		return SCRIPT_CONTINUE;

	if(!utils.isNestedWithinAPlayer(self) && (getOwner(self) == player || player_structure.isAdmin(topMost, player)))
	{
		menu_info_data mid = mi.getMenuItemByType(menu_info_types.ITEM_USE);
		string use_string = getStringObjVar(self, USE_STRING);

		mi.addRootMenu(menu_info_types.ITEM_USE, new string_id("spam", use_string));
	}
	return SCRIPT_CONTINUE;
}

trigger OnObjectMenuSelect(obj_id player, int item)
{
	if(!isIdValid(player))
		return SCRIPT_CONTINUE;

	obj_id topMost = getTopMostContainer(self);
	if(!player_structure.isBuilding(topMost))
		return SCRIPT_CONTINUE;

	//make sure we are still in the world
	if(!utils.isNestedWithinAPlayer(self) && (getOwner(self) == player || player_structure.isAdmin(topMost, player)))
	{
		//get all the customize variables
		custom_var[] allVars = getAllCustomVars(self);

		if(allVars == null || allVars.length <= 0)
			return SCRIPT_CONTINUE;

		//set this scriptvar so we will know this is being fired from this script in the trigger OnCustomizeFinished
		//which is located in base_player
		utils.setScriptVar(player, "veteranRewardItemColor.color_setting", true);

		//get the length of the colors and call the appropriate function
		if(allVars.length == 1)
			openCustomizationWindow(player, self, allVars[0].getVarName(), -1, -1, "", -1, -1, "", -1, -1, "", -1, -1);
		else if (allVars.length == 2)
			openCustomizationWindow(player, self, allVars[0].getVarName(), 0, 10, allVars[1].getVarName(), -1, -1, "", -1, -1, "", -1, -1);
		else if (allVars.length == 3)
			openCustomizationWindow(player, self, allVars[0].getVarName(), -1, -1, allVars[1].getVarName(), -1, -1, allVars[2].getVarName(), -1, -1, "", -1, -1);
		else if (allVars.length == 4)
			openCustomizationWindow(player, self, allVars[0].getVarName(), -1, -1, allVars[1].getVarName(), -1, -1, allVars[2].getVarName(), -1, -1, allVars[3].getVarName(), -1, -1);


	}
	return SCRIPT_CONTINUE;
}


include library.ai_lib;
include library.chat;
include library.combat;
include library.buff;
include ai.ai_combat;
include library.utils;

const string STF_FILE = "quest/pirates";

trigger OnCreatureDamaged(obj_id attacker, obj_id wpn, int[] damage)
{
	//debugSpeakMsg(self, "Ak, I've been hit");
	//int maxState = BEHAVIOR_ATTACK;
	//int maxHealth = getMaxAttrib(self, HEALTH);
	//float healthPercent = ( 100 / maxHealth );
	//int damageTaken = maxHealth - getAttrib( self, HEALTH );
	//float damagePercent = damageTaken * healthPercent;
	//debugSpeakMsg(self, "Looking into damage total");
	//if ( damagePercent > 50 )

	const float DAMAGE_THRESHOLD = 0.50f;

	boolean check = false;

	int maxHealth = getUnmodifiedMaxAttrib(self, HEALTH);
	int currentHealth = getAttrib(self, HEALTH);

	if ( currentHealth <= (maxHealth * DAMAGE_THRESHOLD) )
		check = true;

	/*
	for (int i=0; i<2;i++)
	{
		if (getMaxAttrib(self, i*3)/2 > getAttrib(self, i*3))
			check = true;

	}
	*/

	if(check) {

		if (!hasObjVar(self, "quest.hero_of_tatooine.attacked"))
		{
			setInvulnerable(self, true);
			messageTo(self, "makePirateLeaderInvulnerable", null, 1, false);
			messageTo(self, "makePirateLeaderAttackable", null, 189, false);
			chat.chat(self, new string_id(STF_FILE, "dont_hurt_us"));
		}
	}

	return SCRIPT_CONTINUE;
}

messageHandler attackPlayer ()
{
	clearCondition (self, CONDITION_CONVERSABLE);
	clearCondition (self, CONDITION_INTERESTING);
	detachScript (self, "conversation.quest_hero_of_tatooine_pirate_leader");

	obj_id player = params.getObjId("player");
	if ( isIdValid(player) )
	{
		setInvulnerable(self, false);
		startCombat(self, player);
	}

	return SCRIPT_CONTINUE;
}

messageHandler makePirateLeaderAttackable ()
{
	if ( !isInNpcConversation(self) )
	{
		clearCondition (self, CONDITION_CONVERSABLE);
		clearCondition (self, CONDITION_INTERESTING);

		if ( hasScript(self, "conversation.quest_hero_of_tatooine_pirate_leader") )
		{
			detachScript (self, "conversation.quest_hero_of_tatooine_pirate_leader");
		}

		setInvulnerable(self, false);
	}
	else
	{
		messageTo(self, "makePirateLeaderAttackable", null, 99, false);
	}

	return SCRIPT_CONTINUE;
}


messageHandler makePirateLeaderInvulnerable ()
{
	utils.removeScriptVar( self, "ai.combat.isInCombat");
	setCombatTarget(self, null);
	stopCombat(self);
	ai_lib.setDefaultCalmBehavior( self, ai_lib.BEHAVIOR_SENTINEL );
	buff.removeAllBuffs(self);
	clearHateList(self);
	setCondition (self, CONDITION_CONVERSABLE);
	attachScript(self, "conversation.quest_hero_of_tatooine_pirate_leader");
	setObjVar(self, "quest.hero_of_tatooine.attacked", true);

	return SCRIPT_CONTINUE;
}
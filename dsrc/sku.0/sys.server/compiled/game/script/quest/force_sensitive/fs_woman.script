/**
 * Copyright (c)2000-2004 Sony Online Entertainment Inc.
 * All Rights Reserved
 *
 * Title:        fs_oldman_initial.script
 * Description:  Force Sensitive female quest npc
 * @author       $Author:$
 * @version      $Revision:$
 */

include library.ai_lib;
include library.prose;
include library.chat;
include library.quests;
include library.fs_quests;

trigger OnAttach()
{
	createTriggerVolume("fs_woman_messagerange", 12, true);
	messageTo(self, "approach", null, 3, false);  // Wait 3 seconds and approach the player
	return SCRIPT_CONTINUE;
}	

messageHandler approach()
{
	obj_id owner = null;
	owner = getObjIdObjVar(self, "fs_dath_woman.holder");
	setMovementRun(self);
	
	if ( (isIdValid(owner)) && (exists(owner)) )
	{
		ai_lib.aiFollow(self, owner);
	}
	
	messageTo( self, "approachFail", null, 180, false ); // 180 seconds he cleans up if he hasnt made contact
	return SCRIPT_CONTINUE;
}

trigger OnTriggerVolumeEntered(string volumeName, obj_id target )
{
	if (volumeName == "fs_woman_messagerange")
	{
		obj_id owner = getObjIdObjVar(self, "fs_dath_woman.holder");
		
		if ( (isIdValid(owner)) && (exists(owner)) )
		{
			if (owner == target)
			{
				if (!hasObjVar(self, "contact"))
				{
					setObjVar(self, "contact", 1);
					messageTo( self, "timeout", null, 180, false );  
					prose_package pp = prose.getPackage(new string_id("quest/force_sensitive/intro", "woman_greeting"), target);
					chat.publicChat(self, target, null, null, pp);
				}
			}
		}
	}
	return SCRIPT_CONTINUE;
}

messageHandler approachFail()
{
	if (hasObjVar(self, "contact"))
	{
		return SCRIPT_CONTINUE;
	}
	
	obj_id owner = null;
	owner = getObjIdObjVar(self, "fs_dath_woman.holder");
	
	int quest_id = quests.getQuestId("fs_goto_dath");
	clearCompletedQuest(owner, quest_id);
	quest_id = quests.getQuestId("fs_dath_woman");
	clearCompletedQuest(owner, quest_id);
	quest_id = quests.getQuestId("fs_dath_woman_talk");
	clearCompletedQuest(owner, quest_id);
		
	messageTo( self, "cleanUp", null, 180, false );
	return SCRIPT_CONTINUE;
}

messageHandler timeout()
{
	obj_id owner = null;
	owner = getObjIdObjVar(self, "fs_dath_woman.holder");
	
	if ( (isIdValid(owner)) && (exists(owner)) )
	{
		sendSystemMessage(owner, new string_id("quest/force_sensitive/intro", "woman_leave"));
	}
	int quest_id = quests.getQuestId("fs_goto_dath");
	clearCompletedQuest(owner, quest_id);
	quest_id = quests.getQuestId("fs_dath_woman");
	clearCompletedQuest(owner, quest_id);
	quest_id = quests.getQuestId("fs_dath_woman_talk");
	clearCompletedQuest(owner, quest_id);
	messageTo( self, "cleanUp", null, 5, false );
	return SCRIPT_CONTINUE;
}

messageHandler cleanUp()
{
	destroyObject(self);
	return SCRIPT_CONTINUE;
}

/**
 * Copyright (c)2000-2004 Sony Online Entertainment Inc.
 * All Rights Reserved
 *
 * Title:        fs_datapad.script
 * Description:  given to player to start the next stage of intro quest
 * @author       $Author:$
 * @version      $Revision:$
 */

// No Drop, No trade object
// Check to make sure on use that the user is the owner

include library.pclib;
include library.prose;
include library.utils;
include library.quests;
include library.fs_quests;


trigger OnObjectMenuRequest(obj_id player, menu_info mi)
{
	mi.addRootMenu(menu_info_types.SERVER_MENU1, new string_id("sui", "use"));

  	return SCRIPT_CONTINUE;
}

trigger OnObjectMenuSelect(obj_id player, int item)
{
	if (item == menu_info_types.SERVER_MENU1)
	{
		if ( utils.isOwner(self, player) )
		{
			if (hasObjVar(player, "fs_kickoff_stage"))
			{
				int stage = getIntObjVar(player, "fs_kickoff_stage");
				
				if (stage == 6) // tell them to loot it first
				{
					sendSystemMessage(player, new string_id("quest/force_sensitive/intro","loot_disk1"));
					return SCRIPT_CONTINUE;
				}	
							
				if (stage == 7)
				{
					location here = getLocation(player);
					if (!fs_quests.isValidLocationForEncounter(here))
					{
						sendSystemMessage(player, new string_id("quest/force_sensitive/intro","datapad_failure"));
						return SCRIPT_CONTINUE;
					}	
										
					fs_quests.advanceStage(player);
					quests.activate("fs_village_elder", player, null);
					sendSystemMessage(player, new string_id("quest/force_sensitive/intro","read_disk2"));
					destroyObject(self);  // They got the quest we dont need the object anymore
					if (hasObjVar(player, "theaterRecoveryTarget"))
					{
						removeObjVar(player, "theaterRecoveryTarget");
					}
					return SCRIPT_CONTINUE;	
				}
			}	
		}
		sendSystemMessage(player, new string_id("quest/force_sensitive/intro","read_disk_error"));	
	}			
	return SCRIPT_CONTINUE;
}

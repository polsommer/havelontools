include library.groundquests;
include library.utils;


const string STF						= "som/som_quest";
const string_id ENTER_CODE					= new string_id(STF, "df_keypad_code");
const string_id UNLOCKED					= new string_id(STF, "df_keypad_unlocked");
const string_id UNKNOWN_CODE				= new string_id(STF, "df_keypad_unknown");
const string_id INCORRECT_CODE				= new string_id(STF, "df_keypad_incorrect");
const string_id ALREADY_UNLOCKED			= new string_id(STF, "df_keypad_already");

trigger OnAttach()
{
	if(!hasObjVar(self, "passcode"))
	{
		setObjVar(self, "passcode", "37323");
	}
	return SCRIPT_CONTINUE;
}

trigger OnInitialize()
{
	if(!hasObjVar(self, "passcode"))
		{
			setObjVar(self, "passcode", "37323");
		}
	return SCRIPT_CONTINUE;
}

trigger OnObjectMenuRequest(obj_id player, menu_info mi)
{
	int menu = mi.addRootMenu( menu_info_types.ITEM_USE, ENTER_CODE );

	menu_info_data mid = mi.getMenuItemByType(menu_info_types.ITEM_USE);
		if ( mid == null )
		{
			return SCRIPT_CONTINUE;
		}

		mid.setServerNotify(true);

	return SCRIPT_CONTINUE;
}

trigger OnObjectMenuSelect( obj_id player, int item )
{
	if ( item == menu_info_types.ITEM_USE )
	{
		if(	groundquests.isTaskActive(player, "som_story_arc_chapter_three_01", "mustafar_droidfactory_enter"))
		{
			keypad ( player );
			return SCRIPT_CONTINUE;
		}
		if (groundquests.hasCompletedTask(player, "som_story_arc_chapter_three_01", "mustafar_droidfactory_enter"))
		{
			sendSystemMessage(player, ALREADY_UNLOCKED);
			return SCRIPT_CONTINUE;
		}
		if (groundquests.hasCompletedQuest(player, "som_story_arc_chapter_three_01"))
		{
			sendSystemMessage(player, ALREADY_UNLOCKED);
			return SCRIPT_CONTINUE;
		}
		else
		{
			sendSystemMessage(player, UNKNOWN_CODE);
			return SCRIPT_CONTINUE;
		}
	}

	return SCRIPT_CONTINUE;
}

void keypad( obj_id player )
{
	obj_id self = getSelf();
	int pid = createSUIPage ("Script.Keypad", self, player, "KeypadCallback");
	subscribeToSUIProperty(pid, "result.numberBox", "localtext");
	subscribeToSUIProperty(pid, "buttonEnter", "ButtonPressed");

	showSUIPage (pid);

	return;
}

messageHandler KeypadCallback()
{
	string result = params.getString("result.numberBox" + "." + "localtext");
	string button = params.getString("buttonEnter.ButtonPressed");
	obj_id player = params.getObjId ("player");

	if (!isIdValid(player))
	{
		return SCRIPT_OVERRIDE;
	}

	if ( button == null )
	{
		button = "none";
	}

	if ( result == null )
	{
		result = "0";
	}

	string passkey = getStringObjVar (self, "passcode");

	if (passkey == null || passkey == "")
	{
		passkey = "nothing";
	}

	if (button == "enter")
	{
		//debugSpeakMsg (self, "Passkey was " + passkey);
		if (passkey == result)
		{
			sendSystemMessage(player, UNLOCKED);
			groundquests.sendSignal(player, "mustafar_droidfactory_final");
			return SCRIPT_CONTINUE;
		}
		else
		{
			sendSystemMessage(player, INCORRECT_CODE);
		}
	}
	return SCRIPT_CONTINUE;
}
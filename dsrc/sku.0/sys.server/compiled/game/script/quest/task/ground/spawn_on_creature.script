// ================================================================================
//
// spawn_on_creature.script
// copyright 2006, Sony Online Entertainment LLC
//
// This script goes on a creature spawned by the spawn quest task type.
//
// ================================================================================

include library.ai_lib;
include quest.task.ground.spawn;

// --------------------------------------------------------------------------------

const int maxRetries = 5;

const String retryCountName = "retryCount";

// --------------------------------------------------------------------------------

trigger OnAttach()
{
	dictionary params = new dictionary();
	params.put(retryCountName, 0);
	messageTo(self, "messageSpawnCreatureCleanup", params, spawn.cleanupTime, false);
	return SCRIPT_CONTINUE;
}

// --------------------------------------------------------------------------------

messageHandler messageSpawnCreatureCleanup()
{
	if (isDead(self))
	{
		LOG("QUEST_SPAWN_LOG", "Creature is dead!");
		return SCRIPT_CONTINUE;
	}

	if (ai_lib.isInCombat(self))
	{
		int retryCount = params.getInt(retryCountName) + 1;

		if (retryCount <= maxRetries)
		{
			LOG("QUEST_SPAWN_LOG", "Creature in combat - trying again. retryCount = " + retryCount + "/" + maxRetries);

			params.put(retryCountName, retryCount);
			messageTo(self, "messageSpawnCreatureCleanup", params, spawn.cleanupRetryTime, false);
		}
		else
			LOG("QUEST_SPAWN_LOG", "Creature in combat - no more retries left.");

		return SCRIPT_CONTINUE;
	}

	destroyObject(self);
	return SCRIPT_CONTINUE;
}

// ================================================================================

//------------------------------------------------
// Includes
//------------------------------------------------

include library.player_structure;
include library.utils;
include library.sui;
include library.city;

//------------------------------------------------
// Inherits
//------------------------------------------------

inherits item.structure_deed.player_structure_deed;

//------------------------------------------------
// Constants
//------------------------------------------------

const string_id SID_OBSCENE							= new string_id("player_structure", "obscene");
const string_id SID_NOT_UNIQUE						= new string_id("player_structure", "cityname_not_unique");
const string_id SID_ALREADY_MAYOR					= new string_id("city/city", "already_mayor");

//------------------------------------------------
// handleNoReclaimConfirm
//------------------------------------------------

messageHandler handleNoReclaimConfirm()
{
	utils.removeScriptVarTree(self, "noreclaim");

	if ( params == null || params.isEmpty() )
		return SCRIPT_OVERRIDE;

	int bp = sui.getIntButtonPressed(params);
	if ( bp == sui.BP_CANCEL )
		return SCRIPT_OVERRIDE;

	obj_id player = sui.getPlayerId(params);
	if ( !isIdValid(player) )
		return SCRIPT_OVERRIDE;

	if ( !utils.isNestedWithin(self, player) )
		return SCRIPT_OVERRIDE;

	// Are we already a mayor?
	if ( city.isAMayor( player ) )
	{
		sendSystemMessage( player, SID_ALREADY_MAYOR );
		return SCRIPT_OVERRIDE;
	}

	// Query about city name.
	sui.inputbox( self, player, "@city/city:city_name_d", sui.OK_CANCEL, "@city/city:city_name_t", sui.INPUT_NORMAL, null, "handleSetCityName", null );

	return SCRIPT_OVERRIDE;
}

//------------------------------------------------
// handleSetCityName
//------------------------------------------------

messageHandler handleSetCityName()
{
	// Store the city name.
	obj_id player = sui.getPlayerId(params);
	string cityName = sui.getInputBoxText( params );
	int btn = sui.getIntButtonPressed( params );
	if ( btn == sui.BP_CANCEL )
		return SCRIPT_CONTINUE;

	// No zero length or obscene names.
	if ( (cityName == "") || isNameReserved(cityName) )
	{
		// Ask them about the name.
		sendSystemMessage( player, SID_OBSCENE );
		sui.inputbox( self, player, "@city/city:city_name_d", sui.OK_CANCEL, "@city/city:city_name_t", sui.INPUT_NORMAL, null, "handleSetCityName", null );
		return SCRIPT_CONTINUE;
	}

	if ( cityName.length() > 24 )
		cityName = cityName.substring( 0, 23 );

	// Only unique names.
	if ( !city.isUniqueCityName( cityName ) )
	{
		// Ask them about the name.
		sendSystemMessage( player, SID_NOT_UNIQUE );
		sui.inputbox( self, player, "@city/city:city_name_d", sui.OK_CANCEL, "@city/city:city_name_t", sui.INPUT_NORMAL, null, "handleSetCityName", null );
		return SCRIPT_CONTINUE;
	}

	setObjVar( self, "cityName", cityName );

	// Place the city.
	queueCommand( player, ##"placeStructureMode", self, "", COMMAND_PRIORITY_DEFAULT );

	return SCRIPT_CONTINUE;
}
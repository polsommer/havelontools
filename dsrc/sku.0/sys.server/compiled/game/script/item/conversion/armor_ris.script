/**
 * Copyright (c)2000-2002 Sony Online Entertainment Inc.
 * All Rights Reserved
 *
 * Title:        conversion.medicine.script
 * Description:  This script will be attached to all RIS Armor during CU item conversion
 * @author       Thomas Blair
 * @version      $Revision:$
 */


/***** INCLUDES ********************************************************/
include library.utils;
include library.sui;

/***** CONSTANTS *******************************************************/


/***** TRIGGERS ********************************************************/

trigger OnInitialize()
{
	return SCRIPT_CONTINUE;
} 


trigger OnObjectMenuRequest(obj_id player, menu_info mi)
{
	if (utils.getContainingPlayer(self) == player)
	{
		if (hasObjVar(self, "armor.risDeconstruct"))  //converted RIS
		{
			mi.addRootMenu (menu_info_types.SERVER_MENU1, new string_id("craft_armor_ingredients_d","dismantle_ris"));	
		}
		else detachScript(self, "item.conversion.armor_ris");
	}

  	return SCRIPT_CONTINUE;
}

trigger OnObjectMenuSelect(obj_id player, int item)
{
	if (utils.getContainingPlayer(self) == player)
	{
		if (item == menu_info_types.SERVER_MENU1)
		{
			showConfirmationWindow(player);
		}
	}

	return SCRIPT_CONTINUE;
}


void showConfirmationWindow(obj_id player)
{
	obj_id self = getSelf();

	string prompt = "Are you sure you wish to DESTROY this piece of armor and create an RIS armor segment used in the constrution of RIS armor?";
	string title = "Dismantle RIS Armor Piece";

	closeOldWindow(player);
			 
	int pid = sui.msgbox(self, player, prompt, sui.OK_CANCEL, title, 0,"handleConfirmationSelect");
	setWindowPid(player, pid);

}

void dismantleRis(obj_id player)
{
	obj_id self = getSelf();
	obj_id pInv = utils.getInventoryContainer(player);
	if (getVolumeFree(pInv) <= 1)
	{
		sendSystemMessage(player, new string_id("quest_armorsmith", "inventory_full"));
		return;
	}
	else
	{
		destroyObject(self);
		obj_id segment = createObject("object/tangible/component/armor/armor_segment_ris.iff", pInv, "");
		if (isIdValid(segment))
		{
			setCraftedId(segment, segment);
			setCrafter(segment, player);
			sendSystemMessage(player, new string_id("quest_armorsmith", "segment_issued"));
		}
	}

}

void closeOldWindow(obj_id player)
{
	if ( utils.hasScriptVar(player, "ris_armor.pid"))
	{
		int oldpid = utils.getIntScriptVar(player, "ris_armor.pid");
		forceCloseSUIPage (oldpid);
		utils.removeScriptVar(player, "ris_armor.pid");
	}
}

void setWindowPid(obj_id player, int pid)
{
	if (pid > -1) utils.setScriptVar(player, "ris_armor.pid", pid);
}

messageHandler handleConfirmationSelect()
{
	obj_id player = sui.getPlayerId(params);
	int btn = sui.getIntButtonPressed(params);
	if ( btn == sui.BP_CANCEL )
	{
		utils.removeScriptVar(player, "ris_armor.pid");
		return SCRIPT_CONTINUE;
	}
	dismantleRis(player);
	return SCRIPT_CONTINUE;
}
// ALL SERVING DROID SCRIPTS
// structure/serving_droid_control.script
// creature/serving_droid.script
// item/droid/serving_droid_terminal.script

include library.player_structure;
include library.utils;

const string DROIDTEMPLATE = "object/creature/npc/droid/serving_droid.iff";

//STRING IDs
const string MENU_FILE 				= "pet/pet_menu";
const string_id TERMINAL_NAME			= new string_id(MENU_FILE,"serving_droid_terminal_custom_name");
const string_id ASTROMECH_NAME			= new string_id(MENU_FILE,"serving_droid_custom_name");
const string_id ASTROMECH_BIOLINK_NEEDED	= new string_id(MENU_FILE,"serving_droid_biolink_needed");
const string_id ASTROMECH_NOT_INVENTORY		= new string_id(MENU_FILE,"serving_droid_not_inventory");
const string_id ASTROMECH_NOT_OWNER		= new string_id(MENU_FILE,"serving_droid_not_owner");
const string_id ASTROMECH_NOT_IN_HOUSE		= new string_id(MENU_FILE,"serving_droid_not_in_house");

//SUI MENU STRING IDs
const string_id SUMMON 				= new string_id("sui", "summon_droid");
const string_id DESTROY 			= new string_id("sui", "destroy_droid");

//variable constants
const string OBJVAR_DROID_PATROL_POINTS 	= "droid.patrolPoints";
const string OBJVAR_DROID_PATROL_LOOP 		= "droid.patrol_loop";
const string OBJVAR_DROID_PATROL_ONCE 		= "droid.patrol_once";
const string DROID_ID 				= "droid.id";
const string DROID_NAMED 			= "droid.named";
const string PLAYER 				= "droid.user";

//------------------------------------------------
//triggers
//------------------------------------------------

//If the house is loaded, this will be called
trigger OnInitialize()
{
	obj_id terminal = self;
	obj_id droid = getObjIdObjVar(terminal, DROID_ID);
	//If the droid OID is valid but the Object no longer exists
	if(!isValidId(droid))
	{
		removeObjVar(terminal, DROID_ID);
	}
	else if(!exists(droid))
	{
		boolean created = recreateDroid(terminal);
		if(!created)
			removeObjVar(terminal, DROID_ID);
	}	

	return SCRIPT_CONTINUE;
}


//For whatever reason, if the terminal is destroyed the droid is to
trigger OnDestroy()
{
	obj_id terminal = self;
	obj_id droid = getObjIdObjVar(terminal, DROID_ID);
	if (!isValidId(droid))
		return SCRIPT_CONTINUE;

	destroyObject(droid);
	return SCRIPT_CONTINUE;
}

//This is called if the player picks up the terminal object.  Making sure the droid is destroyed.
trigger OnAboutToBeTransferred(obj_id destContainer, obj_id transferer)
{
	obj_id terminal = self;
	if(!isValidId(terminal))
		return SCRIPT_CONTINUE;
		
	if(hasObjVar(terminal, DROID_ID) && exists(terminal))
	{
		messageTo(terminal, "destroyDroid", null, 1, false);
	}

	//we can't leave the location array on the terminal in case the terminal is moved to another house.
	if (hasObjVar(terminal, OBJVAR_DROID_PATROL_POINTS) && exists(terminal))
	{
		removeObjVar(terminal, OBJVAR_DROID_PATROL_POINTS);
	}
	if (hasObjVar(terminal, OBJVAR_DROID_PATROL_LOOP) && exists(terminal))
	{
		removeObjVar(terminal, OBJVAR_DROID_PATROL_LOOP);
	}
	if (hasObjVar(terminal, OBJVAR_DROID_PATROL_ONCE) && exists(terminal))
	{
		removeObjVar(terminal, OBJVAR_DROID_PATROL_ONCE);
	}
	return SCRIPT_CONTINUE;
}

trigger OnObjectMenuRequest(obj_id player, menu_info mi)
{
	obj_id terminal = self;	
	if(!exists(terminal))
		return SCRIPT_CONTINUE;
	
	if(utils.isInVendor(terminal) || utils.isInBazaar(terminal))
		return SCRIPT_CONTINUE;

	//get the top container
	obj_id ownerContainer = getTopMostContainer(terminal);
	if(!isValidId(ownerContainer) || !exists(ownerContainer))
		return SCRIPT_CONTINUE;

	//make sure container is a player structure, or player inventory if not abort.
	//If the terminal is in a treasure drum or other container default menu options for pickup and examine only
	if(!player_structure.isBuilding(ownerContainer) && !isPlayer(ownerContainer))
		return SCRIPT_CONTINUE;	
		
	if(hasObjVar(terminal, DROID_ID))
	{
		mi.addRootMenu(menu_info_types.ITEM_USE, DESTROY);
	}
	else
	{
		mi.addRootMenu(menu_info_types.ITEM_USE, SUMMON);
	}	
	return SCRIPT_CONTINUE;
}

trigger OnObjectMenuSelect(obj_id player, int item)
{
	obj_id terminal = self;
	obj_id biolink = getBioLink(terminal);
	
	if(!isValidId(terminal))
		return SCRIPT_CONTINUE;
	
	if(!isValidId(player))
		return SCRIPT_CONTINUE;
	
	sendDirtyObjectMenuNotification(terminal);		
	
	if(item != menu_info_types.ITEM_USE)			
		return SCRIPT_CONTINUE;
		
	//Make sure the terminal is BioLinked.  
	if(!isValidId(biolink) || biolink == utils.OBJ_ID_BIO_LINK_PENDING)
	{
		sendSystemMessage(player, ASTROMECH_BIOLINK_NEEDED);
		return SCRIPT_CONTINUE;	
	}
	//Make sure not in inventory
	if(utils.isNestedWithinAPlayer(terminal))
	{
		sendSystemMessage(player, ASTROMECH_NOT_INVENTORY);
		return SCRIPT_CONTINUE;
	}
	//Make sure this is the owner
	if(player != getObjIdObjVar(terminal, "biolink.id"))
	{
		sendSystemMessage(player, ASTROMECH_NOT_OWNER);		
		return SCRIPT_CONTINUE;		
	}
	if(!utils.isInHouseCellSpace(terminal))
	{
		sendSystemMessage(player, ASTROMECH_NOT_IN_HOUSE);		
		return SCRIPT_CONTINUE;		
	}
	if(!hasObjVar(terminal, DROID_ID))
	{
		utils.setScriptVar(terminal, "terminal.summon", true);			
		boolean droidCreated = createDroid(terminal, player);
		if(!droidCreated)
			removeObjVar(terminal, DROID_ID);		
	}
	else
	{
		messageTo(terminal, "destroyDroid", null, 1, false);
	}

	return SCRIPT_CONTINUE;
}

//------------------------------------------------
//messageHandlers
//------------------------------------------------
messageHandler destroyDroid()
{
	obj_id terminal = self;
	if(!isValidId(terminal))
		return SCRIPT_CONTINUE;

	obj_id player = getObjIdObjVar(terminal, "biolink.id");	
	if(!isValidId(player) || !exists(player))
		return SCRIPT_CONTINUE;	
		
	obj_id droid = getObjIdObjVar(terminal, DROID_ID);	
	if(isValidId(droid) && exists(droid))
	{
		obj_id house = getObjIdObjVar(droid, "house");
		if(!isValidId(house) || !exists(house))
			return SCRIPT_CONTINUE;

		removeObjVar(house, "serving_droid.droid");
		if(hasScript(house, "structure.serving_droid_control"))
			detachScript(house, "structure.serving_droid_control");
		
		destroyObject(droid);
	}
	removeObjVar(terminal, DROID_ID);
	sendConsoleMessage(player, "Droid retired.");
	
	return SCRIPT_CONTINUE;
}

boolean nameTerminal(obj_id terminal)
{
	if(!isValidId(terminal))
		return false;
	obj_id user = getObjIdObjVar(terminal, "biolink.id");
	string text = getName(user) + localize(TERMINAL_NAME);
	setName(terminal, text);
	setObjVar(terminal, DROID_NAMED, true);
	return true;
}

boolean setDroidVars(obj_id terminal, obj_id droid)
{
	if(!isValidId(terminal) || !exists(terminal))
		return false;

	if(!isValidId(droid) || !exists(droid))
		return false;

	obj_id user = getObjIdObjVar(terminal, "biolink.id");
	setInvulnerable(droid, true);
	//script automatically attached in the droids template
	setObjVar(terminal, DROID_ID, droid);
	setObjVar(droid, "terminal", terminal);
	setObjVar(droid, PLAYER, user);
	messageTo(droid, "reColorDroid", null, 0, false);
	setName(droid, getName(user) + localize(ASTROMECH_NAME));
	return true;
}

boolean createDroid(obj_id terminal, obj_id player)
{
	//debugSpeakMsg(terminal, "spawning function");
	if(!isValidId(terminal) || !exists(terminal))
		return false;
	
	obj_id anotherDroid = getObjIdObjVar(terminal, DROID_ID);
	//don't spawn more than one droid
	if(isValidId(anotherDroid) && exists(anotherDroid))
		return false;
	

	//debugSpeakMsg(terminal, "so far so good1");
	obj_id house = getTopMostContainer(terminal);
	if(!isIdValid(house) && !exists(house))
		return false;

	if(!player_structure.isBuilding(house))
		return false;	

	//debugSpeakMsg(terminal, "so far so good2");
	//we need to listen for the broadcast and we can't do this 
	//until we know for sure the terminal is biolinked and installed in a house
	if(!hasScript(house, "structure.serving_droid_control"))
	{
		attachScript(house, "structure.serving_droid_control");
		listenToMessage(house, "handleActivateDroid");
	}

	//If the owner never set up the patrol, don't spawn droid
	location startLocation = new location();
	location[] patrolLoc = getLocationArrayObjVar(terminal, OBJVAR_DROID_PATROL_POINTS);	
	if (patrolLoc == null)
	{
		//debugSpeakMsg(terminal, "patrol locations == null");	
		location playerLoc = getLocation(player);
		if(!isValidId(playerLoc.cell))
		{
			startLocation = getLocation(terminal);
		}	
		else
		{
			startLocation = getLocation(player);
		}
	}
	else 
	{
		//validate location array
		boolean validList = validLocationList(terminal, patrolLoc);
		if(!validList)
			return false;

		//start droid at first location	
		startLocation = patrolLoc[0];
	}
	
	obj_id droid = createObject(DROIDTEMPLATE, startLocation);
	if (patrolLoc != null)
		messageTo(droid, "handleActivateDroid", null, 1, false);

	if(!isIdValid(droid) && !exists(droid))
		return false;
		
	setDroidVars(terminal, droid);

	if(utils.hasScriptVar(terminal, "terminal.summon"))
	{
		sendConsoleMessage(player, "Droid summoned.");
		utils.removeScriptVar(terminal, "terminal.summon");
	}
	if(!hasObjVar(terminal, DROID_NAMED))
		nameTerminal(terminal);	
	
	return true;
}

boolean recreateDroid(obj_id terminal)
{	
	obj_id anotherDroid = getObjIdObjVar(terminal, DROID_ID);
	//don't spawn more than one droid
	if(isValidId(anotherDroid) && exists(anotherDroid))
		return false;
	
	obj_id house = getTopMostContainer(terminal);
	if(!isIdValid(house) && !exists(house))
		return false;

	if(!player_structure.isBuilding(house))
		return false;	

	//we need to listen for the broadcast and we can't do this 
	//until we know for sure the terminal is biolinked and installed in a house
	if(!hasScript(house, "structure.serving_droid_control"))
	{
		attachScript(house, "structure.serving_droid_control");
		listenToMessage(house, "handleActivateDroid");
	}

	//If the owner never set up the patrol, don't spawn droid
	location startLocation = new location();
	location[] patrolLoc = getLocationArrayObjVar(terminal, OBJVAR_DROID_PATROL_POINTS);	
	if(patrolLoc == null)
		return false;
	
	//validate location array
	boolean validList = validLocationList(terminal, patrolLoc);
	if(!validList)
		return false;
		
	//start droid at first location
	startLocation = patrolLoc[0];
	
	obj_id droid = createObject(DROIDTEMPLATE, startLocation);
		messageTo(droid, "handleActivateDroid", null, 1, false);

	if(!isIdValid(droid) && !exists(droid))
		return false;
	
	setDroidVars(terminal, droid);

	if(utils.hasScriptVar(terminal, "terminal.summon"))
	{
		utils.removeScriptVar(terminal, "terminal.summon");
	}
	if(!hasObjVar(terminal, DROID_NAMED))
		nameTerminal(terminal);	
	
	return true;
}

boolean validLocationList(obj_id terminal, location[] patrolLocations)
{
	if(patrolLocations == null)
		return false;

	for (int i = 0; i < patrolLocations.length; ++i)
	{
		if(!isValidId(patrolLocations[i].cell))
		{	
			removeObjVar(terminal, OBJVAR_DROID_PATROL_POINTS);
			return false;
		}	
	}
	return true;
}
include library.utils;
include library.static_item;


trigger OnObjectMenuRequest(obj_id player, menu_info mi)
{	
	if (canManipulate(player, self, true, true, 15, true))
	{
		if ( utils.isNestedWithinAPlayer(self) )
		{
			menu_info_data mid = mi.getMenuItemByType(menu_info_types.ITEM_USE);
			if ( mid != null )
				mid.setServerNotify(true);
			else
				mi.addRootMenu(menu_info_types.ITEM_USE, new string_id("spam", "costume_learn"));
		}
	}

	return SCRIPT_CONTINUE;
}

trigger OnObjectMenuSelect(obj_id player, int item)
{
	if( utils.getContainingPlayer(self) != player )
		return SCRIPT_CONTINUE;
	
	//congrats! You clicked on the USE option, lets see if you pass all the checks
	if( item == menu_info_types.ITEM_USE)
	{
		if( isIncapacitated(player) || isDead(player))
		{
			sendSystemMessage(player, new string_id("player_structure", "while_dead"));
			return SCRIPT_CONTINUE;
		}

		string itemName = getStaticItemName(self);
		if ( itemName == null || itemName == "" )
		{
			return SCRIPT_CONTINUE;
		}

		dictionary itemData = new dictionary();
		itemData = dataTableGetRow(static_item.ITEM_STAT_BALANCE_TABLE, itemName);
		if ( itemData == null)
		{
			return SCRIPT_CONTINUE;
		}	
		
		string costumeName = itemData.getString("buff_name");
		//make sure they don't already have it.
		if ( hasSkill(player, costumeName) )
		{
			// "You already know this costume."
			sendSystemMessage(player, new string_id("spam", "costume_already_known"));
		}
		else
		{
			grantSkill(player, costumeName);
			destroyObject(self);
		}
	}
		
	return SCRIPT_CONTINUE;
}

trigger OnGetAttributes(obj_id player, string[] names, string[] attribs)
{
	int idx = utils.getValidAttributeIndex(names);
	if (idx == -1)
		return SCRIPT_CONTINUE;

	string itemName = getStaticItemName(self);
	if ( itemName == null || itemName == "" )
	{
		return SCRIPT_CONTINUE;
	}
	
	dictionary itemData = new dictionary();
	itemData = dataTableGetRow(static_item.ITEM_STAT_BALANCE_TABLE, itemName);
	if ( itemData == null || itemData.isEmpty() )
	{
		return SCRIPT_CONTINUE;
	}
	
	names[idx] = "storyteller_costume_already_known";
	
	string costumeName = itemData.getString("buff_name");
	if ( hasSkill(player, costumeName) )
	{
		attribs[idx] = " \\#B22222 Already Known";
	}
	else
	{
		attribs[idx] = " \\#00FF00 Not Yet Known";
	}

	idx++;
	if (idx >= names.length)
		return SCRIPT_CONTINUE;
	
	return SCRIPT_CONTINUE;

}

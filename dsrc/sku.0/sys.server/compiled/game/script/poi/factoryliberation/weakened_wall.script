/**
 * Copyright (c) ©2000,2001 Sony Online Entertainment Inc.
 * All Rights Reserved
 *
 * Title:        weakened_wall.script
 * Description:  script for destroyable wall: POI = factoryliberation
 * @author       $Author: breinhart $
 * @version      $Revision: #1 $
 */

// Also used by "prisonbreak" POI.

//------------------------------------------------
// Includes
//------------------------------------------------

include library.poi;

//------------------------------------------------
// Constants
//------------------------------------------------

const string LOG_NAME						= "poiFactoryLiberation Weakened Wall";

//------------------------------------------------
// OnObjectDisabled
//
// Notifies the master when the wall is disabled.
//------------------------------------------------

trigger OnObjectDisabled( obj_id killer )
{
	// Notify the POI that a wall has fallen.
	obj_id poiMaster = getObjIdObjVar( self, "poi.baseObject" );
	if ( (poiMaster == null) || (poiMaster == obj_id.NULL_ID) )
		return SCRIPT_CONTINUE;
	messageTo( poiMaster, "wallDestroyed", null, 0, false );

	// Spawn an explosion at that point.
	location loc = getLocation( self );
	obj_id players[] = getPlayerCreaturesInRange( loc, 40 );
	for ( int i=0; i<players.length; i++ )
	{
		if ( (players[i] != null) && (players[i] != obj_id.NULL_ID) )
			playClientEffectLoc( players[i], "clienteffect/combat_grenade_large_01.cef", loc, 0 );
	}

	// Remove us.
	destroyObject( self );

	return SCRIPT_CONTINUE;
}

//------------------------------------------------
// OnCombatDamaged
//
// Notifies the master the first time we are damaged.
//------------------------------------------------

trigger OnObjectDamaged( obj_id attacker, obj_id weapon, int damage )
{
	int hitpoints = getHitpoints(self);

	// Take an extra 200 damage.
	hitpoints -= 200;
	setHitpoints( self, hitpoints );

	int max_hitpoints = getMaxHitpoints(self);

	float percent_hp = (float)hitpoints / (float)max_hitpoints;

	// When damaged for the first time, tell the prisoners to celebrate.
	boolean wallDamaged = getBooleanObjVar( self, "wallDamaged" );
	if ( !wallDamaged )
	{
		setObjVar( self, "wallDamaged", true );
		obj_id poiMaster = getObjIdObjVar( self, "poi.baseObject" );
		if ( (poiMaster == null) || (poiMaster == obj_id.NULL_ID) )
			return SCRIPT_CONTINUE;
		dictionary params = new dictionary();
		params.put( "attacker", attacker );
		messageTo( poiMaster, "wallDamaged", params, 0, false );
	}

	return SCRIPT_CONTINUE;
}